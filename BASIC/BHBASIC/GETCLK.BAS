00010 REM ****************************************************************
00015 REM GETCLK.BAS (BENTON HARBOR BASIC)
00020 REM GET DATE/TIME FROM HAYES CHRONOGRAPH AND SET HDOS DATE/TIME
00025 REM WRITTEN BY J. TRAVIS N6YPC, 24 JAN 2022
00030 REM ****************************************************************
00035 P=250 : REM H8-5 CONSOLE PORT 372Q
00040 OUT P+1,0  : REM CLEAR COMMAND REGISTER
00045 OUT P+1,0  : REM ENSURE RESET TAKES
00050 OUT P+1,64 : REM RESET 8251 USART
00055 OUT P+1,78 : REM MODE N,8,1,X16
00060 OUT P+1,37 : REM CMD RTS,RXE,TXE
00100 O$="ATVT:" : GOSUB 200 : REM SET TIME SEPERATOR
00105 O$="ATVD-" : GOSUB 200 : REM SET DATE SEPERATOR
00110 O$="ATRT"  : GOSUB 200 : REM READ TIME
00115 T$=I$
00120 REM SET HDOS TIME
00125 O$="ATRD"  : GOSUB 200 : REM READ DATE
00130 D$=MID$(I$,4,5)+"-20"+LEFT$(I$,2)
00135 REM SET HDOS DATE
00140 O$="ATRW"  : GOSUB 200 : W=VAL(I$) : REM READ WEEKDAY
00145 FOR I=0 TO W : READ W$ : NEXT I : REM CONVERT WEEKDAY# TO STRING
00150 DATA MONDAY,TUESDAY,WEDNESDAY,THURSDAY,FRIDAY,SATURDAY,SUNDAY
00155 PRINT : PRINT W$;" ";D$;" ";T$
00160 END 
00200 O$=O$+CHR$(13)+CHR$(10) : I=PIN(P)
00205 FOR I=1 TO LEN(O$)
00210 IF (PIN(P+1)AND 1)=0 THEN 210: REM WAIT FOR TxRDY
00215 OUT P,ASC(MID$(O$,I,1))
00220 NEXT I
00225 I$="" : IF (PIN(P+1)AND 2)=0 THEN 225 : REM WAIT FOR RxRDY
00230 I=PIN(P) : IF I=13 THEN 240
00235 I$=I$+CHR$(I) : IF (PIN(P+1)AND 2)>0 THEN 230
00240 RETURN 
