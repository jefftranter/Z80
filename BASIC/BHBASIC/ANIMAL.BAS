00010 REM 
00020 REM 
00030 REM     **********************************
00040 REM     *                                *
00050 REM     *            ANIMALS             *
00060 REM     *           WRITEN BY            *
00070 REM     *       FRED WOITINECK, JR.      *
00080 REM     *        FOR THE HEATH H8        *
00090 REM     *                                *
00100 REM     **********************************
00110 REM 
00120 REM 
00130 REM 
00140 REM 
00150 GOSUB 1080:REM  CLEAR SCREEN
00160 PRINT "THIS IS THE GAME ANIMAL.  YOU THINK OF AN ANIMAL"
00170 PRINT "AN I'LL TRY TO GUESS WHAT IT IS BY ASKING QUESTIONS."
00180 PRINT "WHEN I GIVE UP, I'LL TAKE A 'WILD' GUESS .  IF I AM"
00190 PRINT "RIGHT, THEN I'M REAL HAPPY, BUT IF I AM WRONG, THEN"
00200 PRINT "I'LL ASK YOU TO TYPE IN A GOOD QUESTION THAT MIGHT "
00210 PRINT "HAVE HELPED ME DISTINGUISH THE ANIMAL YOU WERE THINKING"
00220 PRINT "OF FROM OTHER SIMILAR ANIMALS. "
00230 PRINT "  MAKE SURE YOUR QUESTION IS IN THE FORM OF A QUESTION."
00240 PRINT 
00250 M=256: REM  THE MAXIMUM NUMBER OF ANIMALS IN THE MATRIX
00260 PRINT "JUST TYPE RETURN IF YOU DON'T NEED A SPECIAL FILE "
00280 PRINT "WOULD YOU LIKE TO CONTINUE AN OLD DATA LIST OR WOULD YOU"
00290 LINE INPUT "LIKE TO START A NEW FILE (NEW OR OLD ?)  <OLD>  ";T$
00300 IF T$="" OR LEFT$(T$,1)="O" THEN 1130
00310 GOTO 960
00320 GOSUB 1080:REM     CLEAR SCREEN
00330 PRINT "PLEASE WAIT WHILE DATA IS BEING READ "
00340 DIM A$(M),B$(M)
00350 OPEN F$ FOR READ AS FILE #1
00360 FOR X=1 TO M
00370 LINE INPUT #1,A$(X)
00380 LINE INPUT #1,B$(X)
00390 NEXT 
00400 GOSUB 1080:REM   CLEAR SCREEN
00410 REM     
00420 REM         MAIN GAME PROGRAM
00430 REM 
00440 X=1:Y=1
00450 GOSUB 1080:REM  CLEAR SCREEN
00455 PRINT "TYPE 'QUIT' TO END THE GAME "
00460 PRINT A$(Y);
00470 LINE INPUT "  ";T$
00475 IF T$="QUIT" THEN GOTO 780
00480 IF LEFT$(T$,1)="Y" THEN X=X*2
00490 IF LEFT$(T$,1)="N" THEN X=(X*2)+1
00500 IF LEFT$(T$,1)<> "N" AND LEFT$(T$,1) <> "Y" THEN 470
00510 IF A$(X)="*" THEN GOTO 540
00520 Y=X
00530 GOTO 460
00540 REM 
00550 REM   LAST ELEMENT
00560 REM 
00570 PRINT "IS THE ANIMAL YOU ARE THINKING OF A ";B$(Y)
00580 LINE INPUT "";T$
00590 IF LEFT$(T$,1)="Y" THEN GOTO 440
00600 LINE INPUT "WHAT IS THE ANIMAL YOU WERE THINKING OF  ";B$(X)
00610 PRINT 
00620 PRINT "GIVE ME A ONE LINE 'QUESTION' THAT DISTINGUISHES"
00630 PRINT "A ";B$(X);" FROM THE OTHER ANIMALS. "
00640 PRINT 
00650 LINE INPUT "";A$(X)
00660 PRINT 
00670 A$(X*2)="*":A$((X*2)+1)="*"
00730 GOTO 440
00770 REM 
00780 REM  CLOSE OUT
00790 REM 
00800 CLOSE #1
00810 OPEN F1$ FOR WRITE AS FILE #1
00820 FOR X=1 TO M:PRINT #1,A$(X):PRINT #1,B$(X):NEXT X
00830 CLOSE #1
00840 GOSUB 1080:REM  CLEAR SCREEN
00850 PRINT "YOUR ANIMAL DATA FILE HAS BEEN STORED ON DISC "
00860 PRINT "UNDER THE FILE NAME  "
00870 PRINT 
00880 PRINT 
00890 PRINT  "              <  ";F1$;"  >"
00900 PRINT 
00910 PRINT "                BYE !"
00920 END 
00930 REM 
00940 REM    SET UP FOR NEW DATA
00950 REM 
00960 CLEAR A$(:CLEAR B$(
00970 DIM A$(M),B$(M)
00980 A$(1)="DOES THE ANIMAL YOU ARE THINKING OF HAVE HAIR"
00990 B$(1)="DOG"
01000 A$(2)="*":A$(3)="*"
01010 LINE INPUT "PLEASE TYPE IN YOUR FIRST NAME ";F1$
01020 IF F1$="237781231" THEN F1$="ANIMAL.DAT": GOTO 440
01030 REM  THE F$ IS A CODE WORD OR NUMBER WHICH ALLOWS THE MASTER
01040 REM  DATA FILE TO BE RESTARTED.  MAKE A BACKUP OF THIS FILE
01050 REM  IF NEEDED TO KEEP A LARGE DATA BASE BUILDING.
01060 F1$=LEFT$(F1$,7)
01070 GOTO 440
01080 REM 
01090 REM   SUBROUTINE TO CLEAR SCREEN
01100 REM 
01110 PRINT CHR$(26);CHR$(30)
01120 RETURN 
01130 REM 
01140 REM    SET UP TO USE OLD FILE NAME
01150 REM 
01160 PRINT "TYPE IN THE FILE NAME YOU WISH TO USE OR JUST"
01161 LINE INPUT "TYPE RETURN, TO RESUME THE MASTER FILE <ANIMAL.DAT> ";T$
01162 REM  USE A CODE WORD HERE TO KEEP PEOPLE FROM GOOFING UP YOUR MAIN
01163 REM  DATA FILE, IF DESIRED.  ' T$="---" IN NEXT LINE
01164 IF T$="237781231" THEN F1$="ANIMAL.DAT":F$=F1$:GOTO 320
01166 IF T$="" THEN F$="ANIMAL.DAT":LINE INPUT "YOUR NAME? ";F1$:F1$=LEFT$(F1$,7): GOTO 320
01170 F1$=T$:F$=T$
01180 GOTO 320
02000 REM    THIS PROGRAM WILL BUILD A PYRAMID OF QUESTIONS AND
02010 REM    APPLY BINARY TYPE ANSWERS (YES OR NO) TO DETERMINE
02020 REM    WHAT ITS NEXT TRY WILL BE.  AS THE PYRAMID IS BUILT
02030 REM    EACH ANIMAL WILL MOST LIKELY HAVE TO BE ENTERED 
02040 REM    SEVERAL TIMES, BUT AS THE DATA FILE BUILDS YOU WILL
02050 REM    FIND THAT IT BECOMES MORE AND MORE ACCURATE.
02060 REM    IF YOU HAVE PLENTY OF MEMORY, CHANGE THE VALUE OF
02070 REM    'M' AT THE START OF THE PROGRAM TO ALLOW FOR A LARGER
02080 REM    DATA FILE. 
02090 REM        TO GET THE IDEA, MAKE SURE THE ANIMAL.DAT FILE
02100 REM    IS ON THE GAME DISK AND PLAY THE GAME USING THE
02110 REM    'OLD' FILE ANIMAL.DAT.  THEN BY CHANGING THE 
02120 REM    PASS WORDS IN LINES 1020 AND 1164 TO ONES YOU CAN
02130 REM    EASILY REMEMBER, YOU CAN START YOUR OWN ANIMAL.DAT
02140 REM    FILE  FROM SCRATCH.
02150 REM 
02160 REM 
