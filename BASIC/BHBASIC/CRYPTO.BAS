00001 REM -PRINT DIRECTIONS
00002 LINE INPUT "DO YOU WANT INSTRUCTIONS? <NO> ";L2$
00003 IF LEFT$(L2$,1)="Y"THEN GOSUB 3000
00005 CLEAR :K=64
00006 G2=1
00009 REM -MAX. STRING LENGTH=80
00010 DIM S$(80),T$(80),A$(80),C$(80),D$(80)
00015 CNTRL 4,0
00020 PRINT "ENTER A WORD OR PHRASE (BETWEEN 5 AND 80 CHARS.) TO BE CODED.ONLY PUNCTUATION"
00021 PRINT "ALLOWED ARE PERIODS AND COMMAS."
00022 PRINT "MAKE SURE THAT THE OTHER PLAYER(S) ARE NOT WATCHING.":LINE INPUT "ENTER- ";B$
00023 IF LEN(B$)<5THEN PRINT B$;"-IS LESS THAN 5 CHARACTERS!":GOTO 5
00025 LINE INPUT "ENTER A CLUE: ";B1$
00030 FOR I=1TO 11:PRINT :NEXT :L=ASC(B$)
00035 REM C$() CONTAINS THE UNCODED SOLUTION
00040 FOR I=1TO LEN(B$):C$(I)=MID$(B$,I,1):NEXT 
00045 REM FN C1(L) RETURNS A RANDOM # FROM 1 TO 26
00050 DEF FN C1(L)=INT(26*RND(L))+1
00055 REM A$() IS A DUMMY STRING USED TO ENCODE ALPHABET,D$() IS THE CODE
00060 FOR I=1TO 26
00065 F=FN C1(L)
00070 E$=A$(F)
00075 IF E$="<"GOTO 65
00077 REM -AS A$() IS FILLED WITH "<",THE ALPHABET IS ENCODED INTO D$()
00080 D$(I)=CHR$(F+K):A$(F)="<"
00085 NEXT 
00090 FOR J=1TO LEN(B$)
00095 H=ASC(MID$(B$,J,1))-64
00096 IF (H=-20)OR (H=-18)OR (H=-32)OR ((H<27)AND (H>0))GOTO 100
00097 PRINT "YOU USED AN ILLEGAL CHARACTER!":GOTO 5
00098 REM -CHECK FOR COMMAS,SPACES,PERIODS
00100 IF H=-20THEN M$=M$+","
00105 IF H=-32THEN M$=M$+" "
00110 IF H=-18THEN M$=M$+"."
00115 IF (H=-32)OR (H=-20)OR (H=-18)GOTO 125
00120 M$=M$+D$(H):REM M$ IS THE CODED STRING CORRESPONDING TO B$
00125 IF H=-20 THEN N$=N$+","
00130 IF H=-32THEN N$=N$+" "
00135 IF H=-18THEN N$=N$+"."
00140 IF (H=-20)OR (H=-18)OR (H=-32)GOTO 150
00145 N$=N$+"_":REM N$ IS THE SOLUTION STRING TO BE FILLED IN BY THE PLAYER
00150 NEXT 
00155 PRINT :PRINT ,,:PRINT "*** ";B1$;" ***"
00160 PRINT M$
00165 PRINT N$
00170 IF N$=B$THEN Q$=B$:GOTO 2010
00175 PRINT "ENTER A GUESS AT A CODED LETTER(E.G. F=N),OR 'CLUE' FOR A CLUE,OR A SOLUTION:"
00176 LINE INPUT ;Q$
00180 IF Q$="CLUE"THEN GOSUB 1000
00185 IF Q$="CLUE"THEN PRINT :GOTO 155
00190 IF LEN(Q$)=LEN(B$)GOTO 2000
00195 X1=ASC(RIGHT$(Q$,1))-64
00200 IF D$(X1)=LEFT$(Q$,1)GOTO 210
00205 PRINT "WRONG.";LEFT$(Q$,1);" IS NOT ";RIGHT$(Q$,1);".":PRINT :GOTO 155
00210 PRINT "RIGHT!":PRINT :PRINT :PRINT M$
00215 REM S$()IS THE SOLUTION,T$() IS THE CODED STRING
00220 FOR J=1TO LEN(N$):S$(J)=MID$(N$,J,1):T$(J)=MID$(M$,J,1):NEXT 
00225 FOR I=1TO LEN(N$):IF (T$(I)=LEFT$(Q$,1))THEN 240
00230 NEXT I
00235 GOTO 245
00240 S$(I)=RIGHT$(Q$,1):NEXT 
00245 R$="":FOR I=1TO LEN(N$):R$=R$+S$(I):NEXT 
00250 N$=R$
00255 GOTO 165
01000 IF G2>3THEN PRINT "YOU HAVE TAKEN 3 CLUES ALREADY!":RETURN 
01002 LINE INPUT "ENTER A LETTER THAT YOU THINK IS IN THE CRYPTOGRAM: ";G1$
01005 L9=0
01010 PRINT ,"CLUE NUMBER ";G2;"-- ";G1$;" IS ";
01020 FOR I=1TO LEN(M$)
01030 IF G1$=MID$(B$,I,1)THEN L9=1:S$(I)=G1$:GOTO 1035
01033 S$(I)=MID$(N$,I,1)
01035 NEXT 
01040 R$=""
01042 FOR J=1TO LEN(N$):R$=R$+S$(J):NEXT 
01043 N$=R$
01045 IF L9=1THEN PRINT "IN THE CRYPTOGRAM.":G2=G2+1:RETURN 
01050 PRINT "NOT IN THE CRYPTOGRAM.":G2=G2+1:RETURN 
02000 REM CHECK THE SOLUTION
02010 IF Q$=B$THEN PRINT "'";B$;"'  IS THE ANSWER!":GOTO 2030
02020 PRINT "'";Q$;"' IS NOT CORRECT.":PRINT "YOU ONLY GET ONE WRONG GUESS,SO YOU HAVE LOST.":PRINT 
02025 PRINT "THE ANSWER WAS '";B$;"'."
02030 LINE INPUT "DO YOU WANT TO CONTINUE PLAYING?  <YES> ";R1$
02040 IF LEFT$(R1$,1)="N" GOTO 2070
02050 GOTO 5
02060 IF Q$<>B$THEN PRINT "THE SOLUTION WAS:":PRINT B$
02070 PRINT "BYE FOR NOW!":END 
03000 PRINT "THIS GAME IS CALLED CRYPTOGRAM.TWO OR MORE PLAYERS TAKE TURNS ENTERING"
03005 PRINT "A WORD OR A PHRASE ALONG WITH A CLUE TO WHAT THAT WORD OR PHRASE IS."
03010 PRINT :PRINT "FOR EXAMPLE,THE FIRST PLAYER MAY ENTER 'GEORGE WASHINGTON'"
03015 PRINT "AS HIS WORD AND 'AMERICAN HISTORY' AS HIS CLUE. HE THEN HITS RETURN"
03020 PRINT "AND THE COMPUTER WILL ENCODE 'GEORGE WASHINGTON' BY SUBSTITUTING"
03025 PRINT "DIFFERENT LETTERS FOR EACH LETTER IN THE WORD.":PRINT 
03030 LINE INPUT "HIT RETURN FOR MORE DIRECTIONS.";Z2$
03035 PRINT "THE OTHER PLAYER WILL THEN ATTEMPT TO DECODE THE WORD BY GUESSING"
03040 PRINT "AT WHAT EACH LETTER IS.THIS IS DONE BY ENTERING J=K (OR JK) IF"
03045 PRINT "YOU THINK THAT THE LETTER J IN THE CODE REPRESENTS THE LETTER K."
03050 PRINT :PRINT "YOU MAY ALSO ASK FOR A CLUE BY ENTERING 'CLUE'."
03055 PRINT "YOU MAY THEN ASK IF A LETTER IS IN THE SOLUTION(E.G. DOES 'G'"
03060 PRINT "APPEAR IN THE SOLUTION-IT DOES IN 'GEORGE WASHINGTON'-3 TIMES.)"
03065 PRINT "THREE SUCH CLUES ARE ALLOWED."
03067 LINE INPUT "HIT RETURN FOR MORE DIRECTIONS.";Z2$
03070 PRINT "FINALLY-IF YOU THINK THAT YOU KNOW THE ANSWER BEFORE ALL THE LETTERS"
03075 PRINT "IN THE WORD ARE GUESSED-YOU MAY ENTER A SOLUTION BY TYPING YOUR GUESS AND"
03080 PRINT "HITTING RETURN.ONLY ONE SUCH GUESS IS ALLOWED-IF WRONG,YOU LOSE."
03085 PRINT "BEGIN THE GAME BY HITTING RETURN.":PAUSE 
03090 RETURN 
