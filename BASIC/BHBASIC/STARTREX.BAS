00010 REM SUPER STARTREK - MAY 16,1978 - REQUIRES 24K MEMORY
00030 REM 
00040 REM ****        **** STAR TREK ****        ****
00050 REM **** SIMULATION OF A MISSION OF THE STARSHIP ENTERPRISE,
00060 REM **** AS SEEN ON THE STAR TREK TV SHOW.
00070 REM **** ORIGIONAL PROGRAM BY MIKE MAYFIELD, MODIFIED VERSION
00080 REM **** PUBLISHED IN DEC'S "101 BASIC GAMES", BY DAVE AHL.
00090 REM **** MODIFICATIONS TO THE LATTER (PLUS DEBUGGING) BY BOB
00100 REM *** LEEDOM - APRIL & DECEMBER 1974,
00110 REM *** WITH A LITTLE HELP FROM HIS FRIENDS . . .
00120 REM *** COMMENTS, EPITHETS, AND SUGGESTIONS SOLICITED --
00130 REM *** SEND TO:  R. C. LEEDOM
00140 REM ***           WESTINGHOUSE DEFENSE & ELECTRONICS SYSTEMS CNTR.
00150 REM ***           BOX 746, M.S. 338
00160 REM ***           BALTIMORE, MD  21203
00170 REM ***
00180 REM *** CONVERTED TO MICROSOFT 8 K BASIC 3/16/78 BY JOHN GORDERS
00190 REM *** LINE NUMBERS FROM VERSION STREK7 OF 1/12/75 PRESERVED AS
00200 REM *** MUCH AS POSSIBLE WHILE USING MULTIPLE STATEMENTS PER LINE
00205 REM *** SOME LINES ARE LONGER THAN 72 CHARACTERS; THIS WAS DONE
00210 REM *** BY USING "?" INSTEAD OF "PRINT" WHEN ENTERING LINES
00215 REM ***
00220 PRINT CHR$(27);"E":PRINT :PRINT :PRINT :PRINT :PRINT :PRINT :PRINT 
00221 PRINT "                                    ,------*------,"
00222 PRINT "                    ,-------------   '---  ------'"
00223 PRINT "                     '-------- --'      / /"
00224 PRINT "                         ,---' '-------/ /--,"
00225 PRINT "                          '----------------'":PRINT 
00226 PRINT "                    THE USS ENTERPRISE --- NCC-1701"
00227 PRINT :PRINT 
00260 CLEAR 
00270 Z$="                         "
00330 DIM G(8,8),C(9,2),K(3,3),N(3),Z(8,8),D(8)
00370 T=INT(RND(1)*20+20)*100:T0=T:T9=25+INT(RND(1)*10):D0=0:E=3000:E0=E
00440 P=10:P0=P:S9=200:S=0:B9=2:K9=0:X$="":X0$=" IS "
00470 DEF FN U(U)=SQR((K(I,1)-S1)*(K(I,1)-S1)+(K(I,2)-S2)*(K(I,2)-S2))
00475 DEF FN R(R)=INT(RND(R)*7.98+1.01)
00480 REM INITIALIZE ENTERPRISE'S POSITION
00490 Q1=FN R(1):Q2=FN R(1):S1=FN R(1):S2=FN R(1)
00530 FOR I=1TO 9:C(I,1)=0:C(I,2)=0:NEXT I
00540 C(3,1)=-1:C(2,1)=-1:C(4,1)=-1:C(4,2)=-1:C(5,2)=-1:C(6,2)=-1
00600 C(1,2)=1:C(2,2)=1:C(6,1)=1:C(7,1)=1:C(8,1)=1:C(8,2)=1:C(9,2)=1
00670 FOR I=1TO 8:D(I)=0:NEXT I
00710 A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"
00810 REM SETUP WHAT EXHISTS IN GALAXY . . .
00815 REM K3= # KLINGONS  B3= # STARBASES  S3 = # STARS
00820 FOR I=1TO 8:FOR J=1TO 8:K3=0:Z(I,J)=0:R1=RND(1)
00850 IF R1>.98THEN K3=3:K9=K9+3:GOTO 980
00860 IF R1>.95THEN K3=2:K9=K9+2:GOTO 980
00870 IF R1>.80THEN K3=1:K9=K9+1
00980 B3=0:IF RND(1)>.96THEN B3=1:B9=B9+1
01040 G(I,J)=K3*100+B3*10+FN R(1):NEXT J:NEXT I:IF K9>T9THEN T9=K9+1
01100 IF B9<>0THEN 1200
01150 IF G(Q1,Q2)<200THEN G(Q1,Q2)=G(Q1,Q2)+120:K9=K9+1
01160 B9=1:G(Q1,Q2)=G(Q1,Q2)+10:Q1=FN R(1):Q2=FN R(1)
01200 K7=K9:IF B9<>1THEN X$="S":X0$=" ARE "
01210 PRINT CHR$(27);"E";
01230 PRINT "YOUR ORDERS ARE AS FOLLOWS:"
01240 PRINT "     DESTROY THE";K9;"KLINGON WARSHIPS WHICH HAVE INVADED THE"
01252 PRINT "     GALAXY BEFORE THEY CAN ATTACK FEDERATION HEADQUARTERS"
01260 PRINT "     ON STARDATE";T0+T9;".  THIS GIVES YOU";T9;"DAYS.  THERE";X0$
01272 PRINT "    ";B9;"STARBASE";X$;" IN THE GALAXY FOR RESUPPLYING YOUR SHIP."
01280 PRINT :REM PRINT"HIT ANY KEY EXCEPT RETURN WHEN READY TO ACCEPT COMMAND"
01300 I=RND(1):REM IF INP(1)=13 THEN 1300
01310 REM HERE ANY TIME NEW QUADRANT ENTERED
01320 Z4=Q1:Z5=Q2:K3=0:B3=0:S3=0:G5=0:D4=.5*RND(1):Z(Q1,Q2)=G(Q1,Q2)
01390 IF Q1<1OR Q1>8OR Q2<1OR Q2>8THEN 1600
01430 GOSUB 9030:PRINT :IF T0<>T THEN 1490
01460 PRINT "YOUR MISSION BEGINS WITH YOUR STARSHIP LOCATED"
01470 PRINT "IN THE GALACTIC QUADRANT, '";G2$;"'.":GOTO 1500
01490 PRINT "NOW ENTERING ";G2$;" QUADRANT . . ."
01500 PRINT :K3=INT(G(Q1,Q2)*.01):B3=INT(G(Q1,Q2)*.1)-10*K3
01540 S3=G(Q1,Q2)-100*K3-10*B3:IF K3=0THEN 1590
01560 PRINT "COMBAT AREA      CONDITION RED":IF S>200THEN 1590
01580 PRINT "   SHIELDS DANGEROUSLY LOW"
01590 FOR I=1TO 3:K(I,1)=0:K(I,2)=0:NEXT I
01600 FOR I=1TO 3:K(I,3)=0:NEXT I:Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
01660 REM POSITION ENTERPRISE IN QUADRANT, THEN PLACE "K3" KLINGONS, &
01670 REM "B3" STARBASES, & "S3" STARS ELSEWHERE.
01680 A$="<*>":Z1=S1:Z2=S2:GOSUB 8670:IF K3<1THEN 1820
01720 FOR I=1TO K3:GOSUB 8590:A$="+K+":Z1=R1:Z2=R2
01780 GOSUB 8670:K(I,1)=R1:K(I,2)=R2:K(I,3)=S9*(0.5+RND(1)):NEXT I
01820 IF B3<1THEN 1910
01880 GOSUB 8590:A$=">!<":Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB 8670
01910 FOR I=1TO S3:GOSUB 8590:A$=" * ":Z1=R1:Z2=R2:GOSUB 8670:NEXT I
01980 GOSUB 6430
01990 IF S+E>10THEN IF E>10OR D(7)=0THEN 2060
02020 PRINT :PRINT "** FATAL ERROR **   YOU'VE JUST STRANDED YOUR SHIP IN "
02030 PRINT "SPACE":PRINT "YOU HAVE INSUFFICIENT MANEUVERING ENERGY,";
02040 PRINT " AND SHIELD CONTROL":PRINT "IS PRESENTLY INCAPABLE OF CROSS";
02050 PRINT "-CIRCUITING TO ENGINE ROOM!!":GOTO 6220
02060 INPUT "COMMAND? ";A$
02080 FOR I=1TO 9:IF LEFT$(A$,3)<>MID$(A1$,3*I-2,3)THEN 2160
02140 ON I GOTO 2300,1980,4000,4260,4700,5530,5690,7290,6270
02160 NEXT I:PRINT "ENTER ONE OF THE FOLLOWING:"
02180 PRINT "  NAV  (TO SET COURSE)"
02190 PRINT "  SRS  (FOR SHORT RANGE SENSOR SCAN)"
02200 PRINT "  LRS  (FOR LONG RANGE SENSOR SCAN)"
02210 PRINT "  PHA  (TO FIRE PHASERS)"
02220 PRINT "  TOR  (TO FIRE PHOTON TORPEDOES)"
02230 PRINT "  SHE  (TO RAISE OR LOWER SHIELDS)"
02240 PRINT "  DAM  (FOR DAMAGE CONTROL REPORTS)"
02250 PRINT "  COM  (TO CALL ON LIBRARY-COMPUTER)"
02260 PRINT "  XXX  (TO RESIGN YOUR COMMAND)":PRINT :GOTO 1990
02290 REM COURSE CONTROL BEGINS HERE
02300 INPUT "COURSE (0-9)? ";C1:IF C1>=9 THEN C1=C1-8
02310 IF C1>=1AND C1<9THEN 2350
02330 PRINT "   LT. SULU REPORTS, 'INCORRECT COURSE DATA, SIR!'":GOTO 1990
02350 X$="8":IF D(1)<0THEN X$="0.2"
02360 PRINT "WARP FACTOR (0-";X$;")";:INPUT W1:IF D(1)<0AND W1>.2THEN 2470
02380 IF W1>0AND W1<=8THEN 2490
02390 IF W1=0THEN 1990
02420 PRINT "   CHIEF ENGINEER SCOTT REPORTS 'THE ENGINES WON'T TAKE";
02430 PRINT " WARP ";W1;"!'":GOTO 1990
02470 PRINT "WARP ENGINES ARE DAMAGED.  MAXIUM SPEED = WARP 0.2":GOTO 1990
02490 N=INT(W1*8+.5):IF E-N>=0THEN 2590
02500 PRINT "ENGINEERING REPORTS   'INSUFFICIENT ENERGY AVAILABLE"
02510 PRINT "                       FOR MANEUVERING AT WARP";W1;"!'"
02530 IF S<N-E OR D(7)<0 THEN 1990
02550 PRINT "DEFLECTOR CONTROL ROOM ACKNOWLEDGES";S;"UNITS OF ENERGY"
02560 PRINT "                         PRESENTLY DEPLOYED TO SHIELDS."
02570 GOTO 1990
02580 REM KLINGONS MOVE/FIRE ON MOVING STARSHIP . . .
02590 FOR I=1TO K3:IF K(I,3)=0THEN 2700
02610 A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB 8670:GOSUB 8590
02660 K(I,1)=Z1:K(I,2)=Z2:A$="+K+":GOSUB 8670
02700 NEXT I:GOSUB 6000:D1=0:D6=W1:IF W1>=1THEN D6=1
02770 FOR I=1TO 8:IF D(I)>=0THEN 2880
02790 D(I)=D(I)+D6:IF D(I)>-.1AND D(I)<0THEN D(I)=-.1:GOTO 2880
02800 IF D(I)<0THEN 2880
02810 IF D1<>1THEN D1=1:PRINT "DAMAGE CONTROL REPORT:  ";
02840 PRINT TAB(8);:R1=I:GOSUB 8790:PRINT G2$;" REPAIR COMPLETED."
02880 NEXT I:IF RND(1)>.2THEN 3070
02910 R1=FN R(1):IF RND(1)>=.6THEN 3000
02930 D(R1)=D(R1)-(RND(1)*5+1):PRINT "DAMAGE CONTROL REPORT:  ";
02960 GOSUB 8790:PRINT G2$;" DAMAGED":PRINT :GOTO 3070
03000 D(R1)=D(R1)+RND(1)*3+1:PRINT "DAMAGE CONTROL REPORT:  ";
03030 GOSUB 8790:PRINT G2$;" STATE OF REPAIR IMPROVED":PRINT 
03060 REM BEGIN MOVING STARSHIP
03070 A$="   ":Z1=INT(S1):Z2=INT(S2):GOSUB 8670
03110 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):X=S1:Y=S2
03140 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):Q4=Q1:Q5=Q2
03170 FOR I=1TO N:S1=S1+X1:S2=S2+X2:IF S1<1OR S1>=9OR S2<1OR S2>=9THEN 3500
03240 S8=INT(S1)*24+INT(S2)*3-26:IF MID$(Q$,S8,2)="  "THEN 3360
03320 S1=INT(S1-X1):S2=INT(S2-X2):PRINT "WARP ENGINES SHUT DOWN AT ";
03350 PRINT "SECTOR";S1;",";S2;"DUE TO BAD NAVIGATION":GOTO 3370
03360 NEXT I:S1=INT(S1):S2=INT(S2)
03370 A$="<*>":Z1=INT(S1):Z2=INT(S2):GOSUB 8670:GOSUB 3910:T8=1
03430 IF W1<1THEN T8=.1*INT(10*W1)
03450 T=T+T8:IF T>T0+T9THEN 6220
03470 REM SEE IF DOCKED, THEN GET COMMAND
03480 GOTO 1980
03490 REM EXCEEDED QUADRANT LIMITS
03500 X=8*Q1+X+N*X1:Y=8*Q2+Y+N*X2:Q1=INT(X/8):Q2=INT(Y/8):S1=INT(X-Q1*8)
03550 S2=INT(Y-Q2*8):IF S1=0THEN Q1=Q1-1:S1=8
03590 IF S2=0THEN Q2=Q2-1:S2=8
03620 X5=0:IF Q1<1THEN X5=1:Q1=1:S1=1
03670 IF Q1>8THEN X5=1:Q1=8:S1=8
03710 IF Q2<1THEN X5=1:Q2=1:S2=1
03750 IF Q2>8THEN X5=1:Q2=8:S2=8
03790 IF X5=0THEN 3860
03800 PRINT "LT. UHURA REPORTS MESSAGE FROM STARFLEET COMMAND:"
03810 PRINT "  'PERMISSION TO ATTEMPT CROSSING OF GALACTIC PERIMETER"
03820 PRINT "  IS HEREBY *DENIED*.  SHUT DOWN YOUR ENGINES.'"
03830 PRINT "CHIEF ENGINEER SCOTT REPORTS  'WARP ENGINES SHUT DOWN"
03840 PRINT "  AT SECTOR";S1;",";S2;"OF QUADRANT";Q1;",";Q2;".'"
03850 IF T>T0+T9THEN 6220
03860 IF 8*Q1+Q2=8*Q4+Q5THEN 3370
03870 T=T+1:GOSUB 3910:GOTO 1320
03900 REM MANEUVER ENERGY S/R **
03910 E=E-N-10:IF E>=0THEN RETURN 
03930 PRINT "SHIELD CONTROL SUPPLIES ENERGY TO COMPLETE THE MANEUVER."
03940 S=S+E:E=0:IF S<=0THEN S=0
03980 RETURN 
03990 REM LONG RANGE SENSOR SCAN CODE
04000 IF D(3)<0THEN PRINT "LONG RANGE SENSORS ARE INOPERABLE":GOTO 1990
04030 PRINT "LONG RANGE SCAN FOR QUADRANT";Q1;",";Q2
04040 O1$="-------------------":PRINT O1$
04060 FOR I=Q1-1TO Q1+1:N(1)=-1:N(2)=-2:N(3)=-3:FOR J=Q2-1TO Q2+1
04120 IF I>0AND I<9AND J>0AND J<9THEN N(J-Q2+2)=G(I,J):Z(I,J)=G(I,J)
04180 NEXT J:FOR L=1TO 3:PRINT ": ";:IF N(L)<0THEN PRINT "*** ";:GOTO 4230
04210 PRINT RIGHT$(STR$(N(L)+1000),4);
04230 NEXT L:PRINT ":":PRINT O1$:NEXT I:GOTO 1990
04250 REM PHASER CONTROL CODE BEGINS HERE
04260 IF D(4)<0THEN PRINT "PHASERS INOPERATIVE":GOTO 1990
04265 IF K3>0THEN 4330
04270 PRINT "SCIENCE OFFICER SPOCK REPORTS  'SENSORS SHOW NO ENEMY SHIPS"
04280 PRINT "                                IN THIS QUADRANT'":GOTO 1990
04330 IF D(8)<0THEN PRINT "COMPUTER FAILURE HAMPERS ACCURACY"
04350 PRINT "PHASERS LOCKED ON TARGET;  ";
04360 PRINT "ENERGY AVAILABLE =";E;"UNITS"
04370 INPUT "NUMBER OF UNITS TO FIRE? ";X:IF X<=0 THEN 1990
04400 IF E-X<0THEN 4360
04410 E=E-X:IF D(7)<0THEN X=X*RND(1)
04450 H1=INT(X/K3):FOR I=1TO 3:IF K(I,3)<=0THEN 4670
04480 H=INT((H1/FN U(0))*(RND(1)+2)):IF H>.15*K(I,3)THEN 4530
04500 PRINT "SENSORS SHOW NO DAMAGE TO ENEMY AT ";K(I,1);",";K(I,2):GOTO 4670
04530 K(I,3)=K(I,3)-H:PRINT H;"UNIT HIT ON KLINGON AT SECTOR";K(I,1);",";
04550 PRINT K(I,2):IF K(I,3)<=0THEN PRINT "*** KLINGON DESTROYED ***":GOTO 4580
04560 PRINT "   (SENSORS SHOW";K(I,3);"UNITS REMAINING)":GOTO 4670
04580 K3=K3-1:K9=K9-1:Z1=K(I,1):Z2=K(I,2):A$="   ":GOSUB 8670
04650 K(I,3)=0:G(Q1,Q2)=G(Q1,Q2)-100:Z(Q1,Q2)=G(Q1,Q2):IF K9<=0THEN 6370
04670 NEXT I:GOSUB 6000:GOTO 1990
04690 REM PHOTON TORPEDO CODE BEGINS HERE
04700 IF P<=0THEN PRINT "ALL PHOTON TORPEDOES EXPENDED":GOTO 1990
04730 IF D(5)<0THEN PRINT "PHOTON TUBES ARE NOT OPERATIONAL":GOTO 1990
04760 INPUT "PHOTON TORPEDO COURSE (1-9)? ";C1:IF C1=9 THEN C1=1
04780 IF C1>=1AND C1<9THEN 4850
04790 PRINT "ENSIGN CHEKOV REPORTS,  'INCORRECT COURSE DATA, SIR!'"
04800 GOTO 1990
04850 X1=C(C1,1)+(C(C1+1,1)-C(C1,1))*(C1-INT(C1)):E=E-2:P=P-1
04860 X2=C(C1,2)+(C(C1+1,2)-C(C1,2))*(C1-INT(C1)):X=S1:Y=S2
04910 PRINT "TORPEDO TRACK:"
04920 X=X+X1:Y=Y+X2:X3=INT(X+.5):Y3=INT(Y+.5)
04960 IF X3<1OR X3>8OR Y3<1OR Y3>8THEN 5490
05000 PRINT "               ";X3;",";Y3:A$="   ":Z1=X:Z2=Y:GOSUB 8830
05050 IF Z3<>0THEN 4920
05060 A$="+K+":Z1=X:Z2=Y:GOSUB 8830:IF Z3=0THEN 5210
05110 PRINT "*** KLINGON DESTROYED ***":K3=K3-1:K9=K9-1:IF K9<=0THEN 6370
05150 FOR I=1TO 3:IF X3=K(I,1)AND Y3=K(I,2)THEN 5190
05180 NEXT I:I=3
05190 K(I,3)=0:GOTO 5430
05210 A$=" * ":Z1=X:Z2=Y:GOSUB 8830:IF Z3=0THEN 5280
05260 PRINT "STAR AT";X3;",";Y3;"ABSORBED TORPEDO ENERGY.":GOSUB 6000:GOTO 1990
05280 A$=">!<":Z1=X:Z2=Y:GOSUB 8830:IF Z3=0THEN 4760
05330 PRINT "*** STARBASE DESTROYED ***":B3=B3-1:B9=B9-1
05360 IF B9>0OR K9>T-T0-T9THEN 5400
05370 PRINT "THAT DOES IT, CAPTAIN!!  YOU ARE HEREBY RELIEVED OF COMMAND"
05380 PRINT "AND SENTENCED TO 99 STARDATES AT HARD LABOR ON CYGNUS 12!!"
05390 GOTO 6270
05400 PRINT "STARFLEET COMMAND REVIEWING YOUR RECORD TO CONSIDER"
05410 PRINT "COURT MARTIAL!":D0=0
05430 Z1=X:Z2=Y:A$="   ":GOSUB 8670
05470 G(Q1,Q2)=K3*100+B3*10+S3:Z(Q1,Q2)=G(Q1,Q2):GOSUB 6000:GOTO 1990
05490 PRINT "TORPEDO MISSED":GOSUB 6000:GOTO 1990
05520 REM SHIELD CONTROL
05530 IF D(7)<0THEN PRINT "SHIELD CONTROL INOPERABLE":GOTO 1990
05560 PRINT "ENERGY AVAILABLE =";E+S;:INPUT "NUMBER OF UNITS TO SHIELDS? ";X
05580 IF X<0 OR S=X THEN PRINT "<SHIELDS UNCHANGED>":GOTO 1990
05590 IF X<=E+S THEN 5630
05600 PRINT "SHIELD CONTROL REPORTS  'THIS IS NOT THE FEDERATION TREASURY.'"
05610 PRINT "<SHIELDS UNCHANGED>":GOTO 1990
05630 E=E+S-X:S=X:PRINT "DEFLECTOR CONTROL ROOM REPORT:"
05660 PRINT "  'SHIELDS NOW AT";INT(S);"UNITS PER YOUR COMMAND.'":GOTO 1990
05680 REM DAMAGE CONTROL
05690 IF D(6)>=0THEN 5910
05700 PRINT "DAMAGE CONTROL REPORT NOT AVAILABLE":IF D0=0THEN 1990
05720 D3=0:FOR I=1TO 8:IF D(I)<0THEN D3=D3+.1
05760 NEXT I:IF D3=0THEN 1990
05780 PRINT :D3=D3+D4:IF D3>=1THEN D3=.9
05810 PRINT "TECHNICIANS STANDING BY TO EFFECT REPAIRS TO YOUR SHIP,"
05820 PRINT "ESTIMATED TIME TO REPAIR:";.01*INT(100*D3);"STARDATES"
05840 INPUT "WILL YOU AUTHORIZE THE REPAIR ORDER (Y/N)? ";A$
05860 IF A$<>"Y"THEN 1990
05870 FOR I=1TO 8:IF D(I)<0THEN D(I)=0
05890 NEXT I:T=T+D3+.1
05910 PRINT :PRINT "DEVICE             STATE OF REPAIR":FOR R1=1TO 8
05920 GOSUB 8790:PRINT G2$;LEFT$(Z$,25-LEN(G2$));INT(D(R1)*100)*.01
05950 NEXT R1:PRINT :IF D0<>0THEN 5720
05980 GOTO 1990
05990 REM KLINGONS SHOOTING
06000 IF K3<=0THEN RETURN 
06010 IF D0<>0THEN PRINT "STARBASE SHIELDS PROTECT THE ENTERPRISE":RETURN 
06040 FOR I=1TO 3:IF K(I,3)<=0THEN 6200
06060 H=INT((K(I,3)/FN U(1))*(2+RND(1))):S=S-H:K(I,3)=K(I,3)/(3+RND(0))
06080 PRINT H;"UNIT HIT ON ENTERPRISE FROM SECTOR";K(I,1);",";K(I,2)
06090 IF S<=0THEN 6240
06100 PRINT "      <SHIELDS DOWN TO";S;"UNITS>":IF H<20THEN 6200
06120 IF RND(1)>.6OR H/S<=.02THEN 6200
06140 R1=FN R(1):D(R1)=D(R1)-H/S-.5*RND(1):GOSUB 8790
06170 PRINT "DAMAGE CONTROL REPORTS ";G2$;" DAMAGED BY THE HIT'"
06200 NEXT I:RETURN 
06210 REM END OF GAME
06220 PRINT "IT IS STARDATE";T:GOTO 6270
06240 PRINT :PRINT "THE ENTERPRISE HAS BEEN DESTROYED.  THE FEDERATION ";
06250 PRINT "WILL BE CONQUERED":GOTO 6220
06270 PRINT "THERE WERE";K9;"KLINGON BATTLE CRUISERS LEFT AT"
06280 PRINT "THE END OF YOUR MISSION."
06290 PRINT :PRINT :IF B9=0THEN 6360
06310 PRINT "THE FEDERATION IS IN NEED OF A NEW STARSHIP COMMANDER"
06320 PRINT "FOR A SIMILAR MISSION -- IF THERE IS A VOLUNTEER,"
06330 INPUT "LET HIM STEP FORWARD AND ENTER 'AYE' ";A$:IF A$="AYE"THEN 10
06360 END 
06370 PRINT "CONGRULATIONS, CAPTAIN!  THE LAST KLINGON BATTLE CRUISER"
06380 PRINT "MENACING THE FEDERATION HAS BEEN DESTROYED.":PRINT 
06400 PRINT "YOUR EFFICIENCY RATING IS";1000*((K7/(T-T0))*(K7/(T-T0)))
06410 GOTO 6290
06420 REM SHORT RANGE SENSOR SCAN & STARTUP SUBROUTINE
06430 FOR I=S1-1TO S1+1:FOR J=S2-1TO S2+1
06450 IF INT(I+.5)<1OR INT(I+.5)>8OR INT(J+.5)<1OR INT(J+.5)>8THEN 6540
06490 A$=">!<":Z1=I:Z2=J:GOSUB 8830:IF Z3=1THEN 6580
06540 NEXT J:NEXT I:D0=0:GOTO 6650
06580 D0=1:C$="DOCKED":E=E0:P=P0
06620 PRINT "SHIELDS DROPPED FOR DOCKING PURPOSES":S=0:GOTO 6720
06650 IF K3>0THEN C$="*RED*":GOTO 6720
06660 C$="GREEN":IF E<E0*.1THEN C$="YELLOW"
06720 IF D(2)>=0THEN 6770
06730 PRINT :PRINT "*** SHORT RANGE SENSORS ARE OUT ***":PRINT :RETURN 
06770 O1$="   |-1-|-2-|-3-|-4-|-5-|-6-|-7-|-8-|":PRINT O1$:FOR I=1 TO 8
06775 PRINT I;
06820 FOR J=(I-1)*24+1TO (I-1)*24+22STEP 3:PRINT " ";MID$(Q$,J,3);:NEXT J
06825 PRINT " ";I;
06830 ON I GOTO 6850,6900,6960,7020,7070,7120,7180,7240
06850 PRINT "        STARDATE          ";INT(T*10)*.1:GOTO 7260
06900 PRINT "        CONDITION          ";C$:GOTO 7260
06960 PRINT "        QUADRANT          ";Q1;",";Q2:GOTO 7260
07020 PRINT "        SECTOR            ";S1;",";S2:GOTO 7260
07070 PRINT "        PHOTON TORPEDOES  ";INT(P):GOTO 7260
07120 PRINT "        TOTAL ENERGY      ";INT(E+S):GOTO 7260
07180 PRINT "        SHIELDS           ";INT(S):GOTO 7260
07240 PRINT "        KLINGONS REMAINING";INT(K9)
07260 NEXT I:PRINT O1$:RETURN 
07280 REM LIBRARY COMPUTER CODE
07290 IF D(8)<0THEN PRINT "COMPUTER DISABLED":GOTO 1990
07320 INPUT "COMPUTER ACTIVE. WHICH OPTION (0-5)? ";A:IF A<0 THEN 1990
07350 PRINT :H8=1:ON A+1GOTO 7540,7900,8070,8500,8150,7400
07360 PRINT "FUNCTIONS AVAILABLE FROM LIBRARY-COMPUTER:"
07370 PRINT "   0 = CUMULATIVE GALACTIC RECORD"
07372 PRINT "   1 = STATUS REPORT"
07374 PRINT "   2 = PHOTON TORPEDO DATA"
07376 PRINT "   3 = STARBASE NAV DATA"
07378 PRINT "   4 = DIRECTION/DISTANCE CALCULATOR"
07380 PRINT "   5 = GALAXY 'REGION NAME' MAP":PRINT :GOTO 7320
07390 REM SETUP TO CHANGE CUM GAL RECORD TO GALAXY MAP
07400 H8=0:G5=1:PRINT "                        THE GALAXY":GOTO 7550
07530 REM CUM GALACTIC RECORD
07540 REM INPUT"DO YOU WANT A HARDCOPY? IS THE TTY ON (Y/N)";A$
07542 REM IFA$="Y"THENPOKE1229,2:POKE1237,3:NULL1
07543 PRINT :PRINT "        ";
07544 PRINT "COMPUTER RECORD OF GALAXY FOR QUADRANT";Q1;",";Q2
07546 PRINT 
07550 PRINT "       1     2     3     4     5     6     7     8"
07560 O1$="     ----- ----- ----- ----- ----- ----- ----- -----"
07570 PRINT O1$:FOR I=1TO 8:PRINT I;:IF H8=0THEN 7740
07630 FOR J=1TO 8:PRINT "   ";:IF Z(I,J)=0THEN PRINT "***";:GOTO 7720
07700 PRINT MID$(STR$(Z(I,J)+1000),3,3);
07720 NEXT J:GOTO 7850
07740 Z4=I:Z5=1:GOSUB 9030:J0=INT(15-.5*LEN(G2$)):PRINT TAB(J0);G2$;
07800 Z5=5:GOSUB 9030:J0=INT(39-.5*LEN(G2$)):PRINT TAB(J0);G2$;
07850 PRINT :PRINT O1$:NEXT I:PRINT :GOTO 1990
07890 REM STATUS REPORT
07900 PRINT "   STATUS REPORT:":X$="":IF K9>1THEN X$="S"
07940 PRINT "KLINGON";X$;" LEFT: ";K9
07960 PRINT "MISSION MUST BE COMPLETED IN";.1*INT((T0+T9-T)*10);"STARDATES"
07970 X$="S":IF B9<2THEN X$="":IF B9<1THEN 8010
07980 PRINT "THE FEDERATION IS MAINTAINING";B9;"STARBASE";X$;" IN THE GALAXY"
07990 GOTO 5690
08010 PRINT "YOUR STUPIDITY HAS LEFT YOU ON YOUR OWN IN"
08020 PRINT "  THE GALAXY -- YOU HAVE NO STARBASES LEFT!":GOTO 5690
08060 REM TORPEDO, BASE NAV, D/D CALCULATOR
08070 IF K3<=0THEN 4270
08080 X$="":IF K3>1THEN X$="S"
08090 PRINT "FROM ENTERPRISE TO KLINGON BATTLE CRUISER";X$
08100 H8=0:FOR I=1TO 3:IF K(I,3)<=0THEN 8480
08110 W1=K(I,1):X=K(I,2)
08120 C1=S1:A=S2:GOTO 8220
08150 PRINT "DIRECTION/DISTANCE CALCULATOR:"
08160 PRINT "YOU ARE AT QUADRANT ";Q1;",";Q2;" SECTOR ";S1;",";S2
08170 PRINT "PLEASE ENTER":INPUT "  INITIAL COORDINATES (X,Y) ";C1,A
08200 INPUT "  FINAL COORDINATES (X,Y) ";W1,X
08220 X=X-A:A=C1-W1:IF X<0THEN 8350
08250 IF A<0THEN 8410
08260 IF X>0THEN 8280
08270 IF A=0THEN C1=5:GOTO 8290
08280 C1=1
08290 IF ABS(A)<=ABS(X)THEN 8330
08310 PRINT "DIRECTION =";C1+(((ABS(A)-ABS(X))+ABS(A))/ABS(A)):GOTO 8460
08330 PRINT "DIRECTION =";C1+(ABS(A)/ABS(X)):GOTO 8460
08350 IF A>0THEN C1=3:GOTO 8420
08360 IF X<>0THEN C1=5:GOTO 8290
08410 C1=7
08420 IF ABS(A)>=ABS(X)THEN 8450
08430 PRINT "DIRECTION =";C1+(((ABS(X)-ABS(A))+ABS(X))/ABS(X)):GOTO 8460
08450 PRINT "DIRECTION =";C1+(ABS(X)/ABS(A))
08460 PRINT "DISTANCE =";SQR(X*X+A*A):IF H8=1THEN 1990
08480 NEXT I:GOTO 1990
08500 IF B3<>0THEN PRINT "FROM ENTERPRISE TO STARBASE:":W1=B4:X=B5:GOTO 8120
08510 PRINT "MR. SPOCK REPORTS,  'SENSORS SHOW NO STARBASES IN THIS";
08520 PRINT " QUADRANT.'":GOTO 1990
08580 REM FIND EMPTY PLACE IN QUADRANT (FOR THINGS)
08590 R1=FN R(1):R2=FN R(1):A$="   ":Z1=R1:Z2=R2:GOSUB 8830:IF Z3=0THEN 8590
08600 RETURN 
08660 REM INSERT IN STRING ARRAY FOR QUADRANT
08670 S8=INT(Z2-.5)*3+INT(Z1-.5)*24+1
08675 IF LEN(A$)<>3THEN PRINT "ERROR":STOP 
08680 IF S8=1THEN Q$=A$+RIGHT$(Q$,189):RETURN 
08690 IF S8=190THEN Q$=LEFT$(Q$,189)+A$:RETURN 
08700 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8):RETURN 
08780 REM PRINTS DEVICE NAME
08790 ON R1GOTO 8792,8794,8796,8798,8800,8802,8804,8806
08792 G2$="WARP ENGINES":RETURN 
08794 G2$="SHORT RANGE SENSORS":RETURN 
08796 G2$="LONG RANGE SENSORS":RETURN 
08798 G2$="PHASER CONTROL":RETURN 
08800 G2$="PHOTON TUBES":RETURN 
08802 G2$="DAMAGE CONTROL":RETURN 
08804 G2$="SHIELD CONTROL":RETURN 
08806 G2$="LIBRARY-COMPUTER":RETURN 
08820 REM STRING COMPARISON IN QUADRANT ARRAY
08830 Z1=INT(Z1+.5):Z2=INT(Z2+.5):S8=(Z2-1)*3+(Z1-1)*24+1:Z3=0
08890 IF MID$(Q$,S8,3)<>A$THEN RETURN 
08900 Z3=1:RETURN 
09010 REM QUADRANT NAME IN G2$ FROM Z4,Z5 (=Q1,Q2)
09020 REM CALL WITH G5=1 TO GET REGION NAME ONLY
09030 IF Z5<=4THEN ON Z4GOTO 9040,9050,9060,9070,9080,9090,9100,9110
09035 GOTO 9120
09040 G2$="ANTARES":GOTO 9210
09050 G2$="RIGEL":GOTO 9210
09060 G2$="PROCYON":GOTO 9210
09070 G2$="VEGA":GOTO 9210
09080 G2$="CANOPUS":GOTO 9210
09090 G2$="ALTAIR":GOTO 9210
09100 G2$="SAGITTARIUS":GOTO 9210
09110 G2$="POLLUX":GOTO 9210
09120 ON Z4GOTO 9130,9140,9150,9160,9170,9180,9190,9200
09130 G2$="SIRIUS":GOTO 9210
09140 G2$="DENEB":GOTO 9210
09150 G2$="CAPELLA":GOTO 9210
09160 G2$="BETELGEUSE":GOTO 9210
09170 G2$="ALDEBARAN":GOTO 9210
09180 G2$="REGULUS":GOTO 9210
09190 G2$="ARCTURUS":GOTO 9210
09200 G2$="SPICA"
09210 IF G5<>1THEN ON Z5GOTO 9230,9240,9250,9260,9230,9240,9250,9260
09220 RETURN 
09230 G2$=G2$+" I":RETURN 
09240 G2$=G2$+" II":RETURN 
09250 G2$=G2$+" III":RETURN 
09260 G2$=G2$+" IV":RETURN 
