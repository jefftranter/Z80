00010 REM BATTLESHIP GAME FOR THE H-8
00015 REM AUTHOR FRANK R NEWCOMER 05/20/78
00050 DIM A(9,9),B$(9,9),C$(9),N$(9),T(50,4)
00060 DATA " ","A","B","C","D","E","F","G","H","I"
00065 DATA " ","1","2","3","4","5","6","7","8","9"
00100 REM ***** OUTPUT INSTRUCTIONS
00101 PRINT "THE GAME OF BATTLESHIP IS PLAYED ON A 9X9 GRID."
00102 PRINT "OBJECT OF THE GAME IS TO LOCATE AND SINK THE"
00103 PRINT "SHIPS HIDDEN ON THE GRID."
00104 PRINT 
00105 PRINT "THE GRID WILL BE IDENTIFIED WITH LETTERS ALONG THE"
00106 PRINT "LEFT SIDE AND NUMBERS ACROSS THE TOP. WHEN YOU"
00107 PRINT "ARE REQUESTED TO ENTER YOUR GUESS - YOU SHOULD ENTER"
00108 PRINT "A LETTER AND NUMBER COMBINATION, SUCH AS A1 INDICATING"
00109 PRINT "FIRST ROW - FIRST COLUMN. OR PERHAPS B6 INDICATING"
00110 PRINT "SECOND ROW - SIXTH COLUMN."
00111 PRINT "CONTINUE?":PAUSE 
00120 PRINT 
00121 PRINT "THERE WILL BE FROM 4 TO 8 SHIPS HIDDEN ON THE GRID."
00122 PRINT "EACH SHIP WILL BE FROM 1 TO 6 UNITS LONG."
00123 PRINT 
00124 PRINT "THE STANDARD VERSION OF THE GAME REQUIRES THAT YOU HIT"
00125 PRINT "ALL PARTS OF A SHIP TO SINK IT (A SHIP SIX UNITS LONG"
00126 PRINT "WOULD REQUIRE SIX HITS TO SINK IT). YOU MAY HOWEVER,"
00127 PRINT "PLAY A MODIFIED VERSION, IN WHICH A SINGLE HIT"
00128 PRINT "ANYWHERE ON A SHIP WILL SINK IT."
00129 PRINT 
00130 PRINT "DO YOU WISH TO PLAY THE MODIFIED VERSION? (YES OR NO) "
00131 LINE INPUT " ";F1$
00132 IF F1$="YES" GOTO 140
00133 IF F1$="NO" GOTO 140
00134 PRINT "I NEED A YES OR NO ANSWER TO THE QUESTION."
00135 PRINT "PLEASE READ THE INSTRUCTIONS CAREFULLY."
00136 GOTO 123
00140 PRINT "YOU HAVE THE OPTION TO SUPPRESS OUTPUT"
00141 PRINT "OF THE GRID. NORMALLY, THE GRID WILL BE DISPLAYED"
00142 PRINT "AFTER EACH MOVE - THIS WORKS WELL IF YOU ARE USING"
00143 PRINT "A CRT TERMINAL. HOWEVER, IF YOU ARE USING"
00144 PRINT "A TELETYPE TERMINAL, SUCH FREQUENT DISPLAYS WILL"
00145 PRINT "USE AN EXCESSIVE AMOUNT OF PAPER."
00146 PRINT "IF YOU DO SUPPRESS OUTPUT OF THE GRID - YOU WILL BE"
00147 PRINT "ABLE TO CHECK IT AT ANY TIME DURING THE GAME"
00148 PRINT "BY ENTERING THE WORD - DISPLAY."
00149 PRINT 
00150 PRINT "DO YOU WISH TO SUPPRESS OUTPUT OF THE GRID? (YES OR NO)"
00151 LINE INPUT " ";F2$
00152 IF F2$="YES" GOTO 180
00153 IF F2$="NO" GOTO 180
00154 PRINT "I NEED A YES OR NO ANSWER."
00155 PAUSE 2000
00156 GOTO 140
00180 FOR I=1 TO 10
00181 READ C$(I-1)
00182 NEXT I
00185 FOR I=1 TO 10
00186 READ N$(I-1)
00187 NEXT I
00200 REM ***** CALC NUMBER OF SHIPS AND SET-UP GAME
00205 I9=INT(8*RND(1))+1
00210 IF I9<4 THEN I9=4
00211 PRINT "THERE WILL BE";I9;"SHIPS IN THIS GAME."
00212 PRINT "I WILL NOW HIDE THE SHIPS AND SET-UP THE GAME."
00213 PRINT "THIS WILL TAKE ME A LITTLE WHILE, SO PLEASE -"
00214 PRINT "BE PATIENT."
00215 PRINT :PRINT :PRINT :PRINT :PRINT :PRINT 
00216 PRINT "YOU MAY STOP THE GAME AT ANY TIME BY ENTERING - I QUIT."
00230 REM ----- ZERO THE ARRAYS
00231 FOR I=1 TO 10
00232 I1=I-1
00233 FOR J=1 TO 10
00234 J1=J-1
00235 A(I1,J1)=0
00236 B$(I1,J1)="."
00237 NEXT J
00238 B$(I1,0)=C$(I1)
00239 NEXT I
00240 FOR J=1 TO 10
00241 J1=J-1
00242 B$(0,J1)=N$(J1)
00243 NEXT J
00250 M5=3
00255 R=0
00260 FOR I=1 TO 50
00261 FOR J=1 TO 4
00262 T(I,J)=0
00263 NEXT J
00264 NEXT I
00270 REM ----- HIDE THE SHIPS
00271 REM X,Y COORD., Z DIRECTION, AND L LENGTH
00272 G=0
00273 H=0
00274 S9=0
00275 T1=0
00280 FOR I=1 TO I9
00281 L=INT(6*RND(1))+1
00282 IF L<3 THEN L=3
00290 Z=INT(4*RND(1))+1
00295 X=INT(9*RND(1))+1
00296 IF X>7 GOTO 295
00300 Y=INT(9*RND(1))+1
00301 IF Y>7 GOTO 300
00310 IF A(X,Y)<>0 GOTO 295
00311 A(X,Y)=I
00312 T1=T1+1
00313 T(T1,1)=I
00314 T(T1,2)=X
00315 T(T1,3)=Y
00320 FOR K=2 TO L
00325 ON Z GOTO 330,335,340,345
00330 X=X+1
00331 GOTO 350
00335 Y=Y+1
00336 GOTO 350
00340 X=X+1
00341 Y=Y+1
00342 GOTO 350
00345 X=X+1
00346 Y=Y-1
00350 IF X>9 GOTO 399
00351 IF Y>9 GOTO 399
00352 IF X=0 GOTO 399
00353 IF Y=0 GOTO 399
00355 IF A(X,Y)<>0 GOTO 399
00360 A(X,Y)=I
00361 T1=T1+1
00362 T(T1,1)=I
00363 T(T1,2)=X
00364 T(T1,3)=Y
00365 NEXT K
00399 NEXT I
00400 REM ***** PRINT OUT GRID
00405 PRINT 
00410 FOR I=1 TO 10
00411 I1=I-1
00415 IF I<9 GOTO 420
00416 IF I=10 GOTO 419
00417 PRINT "SHOTS ";G;
00418 GOTO 420
00419 PRINT "HITS  ";H;
00420 PRINT TAB(10);B$(I1,0);SPC(4);B$(I1,1);SPC(4);B$(I1,2);
00421 PRINT SPC(4);B$(I1,3);SPC(4);B$(I1,4);SPC(4);B$(I1,5);
00422 PRINT SPC(4);B$(I1,6);SPC(4);B$(I1,7);SPC(4);B$(I1,8);
00423 PRINT SPC(4);B$(I1,9)
00425 NEXT I
00450 REM ***** GET GUESS
00451 LINE INPUT "YOUR GUESS? ";G$
00460 IF G$="I QUIT" GOTO 905
00461 IF G$="DISPLAY" GOTO 405
00470 X$=LEFT$(G$,1)
00471 FOR X=1 TO 9
00472 IF X$=C$(X) GOTO 480
00473 NEXT X
00475 PRINT "INPUT INCORRECT - TRY AGAIN"
00476 GOTO 451
00480 Y$=RIGHT$(G$,1)
00481 FOR Y=1 TO 9
00482 IF Y$=N$(Y) GOTO 505
00483 NEXT Y
00484 GOTO 475
00500 REM ***** ANALYSIS INPUT
00505 IF B$(X,Y)="." GOTO 510
00506 PRINT "YOU ALREADY TRIED THAT SPOT, DUMMY! NOW TRY AGAIN."
00507 GOTO 451
00510 G=G+1
00511 B$(X,Y)="!"
00512 IF A(X,Y)<>0 GOTO 600
00513 R=R+1
00514 IF R<5 THEN 517
00515 PRINT "YOU HAVE NOW MISSED";R;"TIMES IN A ROW."
00516 GOTO 590
00517 M=INT(18*RND(1))+1
00518 IF M=M5 GOTO 517
00519 M5=M
00520 IF M>9 GOTO 1000
00521 ON M GOTO 525,530,535,540,545,550,555,560,565
00525 PRINT "SPLASH, YOU MISSED."
00526 GOTO 580
00530 PRINT "GUESS WHAT? YOU MISSED."
00531 GOTO 580
00535 PRINT "ANOTHER SHOT INTO THE DRINK. YOU MISSED."
00536 GOTO 580
00540 PRINT "GREAT SHOT - WE'LL HAVE FISH FOR LUNCH. NOW LET'S SEE"
00541 PRINT "IF YOU CAN HIT A SHIP."
00542 GOTO 580
00545 PRINT "IT'S GOOD YOU DON'T HAVE TO BUY YOUR SHELLS - YOU MISSED."
00546 GOTO 580
00550 PRINT "THE NAVY WANTS YOU TO TAKE A REFRESHER COURSE - YOU MISSED."
00551 GOTO 580
00555 PRINT "IF YOU DID THIS FOR A LIVING YOU WOULD BE IN BIG TROUBLE."
00556 PRINT "YOU MISSED."
00557 GOTO 580
00560 PRINT "YOU JUST SANK TWO LOVERS IN A CANOE - SORRY NO POINTS FOR THAT."
00561 GOTO 580
00565 PRINT "HOORAY! YIPPEE! YOU MISSED."
00580 IF F2$="YES" GOTO 451
00581 PAUSE 2000
00585 GOTO 405
00590 IF R=10 GOTO 594
00591 IF R=15 GOTO 596
00592 IF R=20 GOTO 598
00593 GOTO 580
00594 PRINT "DON'T YOU FEEL SILLY?"
00595 GOTO 580
00596 PRINT "REMEMBER, IF YOU GIVE UP - ENTER I QUIT."
00597 GOTO 580
00598 PRINT "SURPRISE! YOU HAVE JUST BEEN NAMED 'WORST SHOT OF THE YEAR'."
00599 GOTO 580
00600 PRINT "DIRECT HIT!"
00601 B$(X,Y)="X"
00602 H=H+1
00603 R=0
00604 REM ***** PROCESS TABLE
00605 FOR I=1 TO T1
00606 IF T(I,2)<>X GOTO 610
00607 IF T(I,3)<>Y GOTO 610
00608 GOTO 620
00610 NEXT I
00611 PRINT "LOGIC PROBLEM - CALL A PROGRAMMER"
00612 STOP 
00620 T(I,4)=1
00621 REM ***** CHECK FOR SHIP SUNK
00622 S=T(I,1)
00623 IF F1$="YES" GOTO 640
00625 FOR I=1 TO T1
00626 IF T(I,1)=S GOTO 635
00630 NEXT I
00631 GOTO 640
00635 IF T(I,4)=0 GOTO 637
00636 GOTO 630
00637 IF F2$="YES" GOTO 451
00638 GOTO 405
00640 PRINT "SHIP NUMBER";S;"HAS BEEN SUNK."
00641 S9=S9+1
00642 IF I9-S9=0 GOTO 648
00643 IF I9-S9=1 GOTO 646
00644 PRINT I9-S9;"SHIPS TO GO!"
00645 GOTO 651
00646 PRINT "ONLY ONE MORE SHIP TO FIND. HAVING FUN?"
00647 GOTO 651
00648 PRINT 
00650 REM ***** OUTPUT NUMBER TO GRID
00651 FOR I=1 TO T1
00652 IF T(I,1)<>S GOTO 656
00653 X=T(I,2)
00654 Y=T(I,3)
00655 B$(X,Y)=N$(S)
00656 NEXT I
00660 REM ***** CHECK FOR ALL SHIPS SUNK
00661 IF I9-S9<>0 GOTO 637
00700 REM ***** END OF GAME
00701 PRINT :PRINT :PRINT 
00702 PRINT 
00710 PRINT "TERRIFIC! YOU HAVE SUNK ALL THE SHIPS."
00720 PRINT "OUT OF";G;"SHOTS - YOU HAD";H;"HITS."
00721 P=INT((H/G)*100)
00730 PRINT "SO YOU HIT";P;"% OF YOUR SHOTS."
00731 IF P>10 GOTO 735
00732 PRINT "YOU NEED MORE PRACTICE, RIGHT?"
00733 GOTO 749
00735 IF P>20 GOTO 740
00736 PRINT "YOU PLAYED A VERY GOOD GAME."
00737 GOTO 749
00740 IF P>30 GOTO 745
00741 PRINT "YOU ARE AN EXCELLENT PLAYER."
00742 GOTO 749
00745 PRINT "WITH A SCORE LIKE THAT YOU ARE READY TO PLAY STAR WARS!"
00748 PRINT 
00749 PRINT :PRINT :PRINT 
00750 LINE INPUT "DO YOU WISH TO PLAY AGAIN? (YES OR NO) ";A$
00755 IF A$="YES" GOTO 205
00756 IF A$="NO" GOTO 950
00760 PRINT "I DON'T UNDERSTAND THAT ANSWER."
00761 GOTO 750
00900 REM ***** PRINT OUT SHIP LOCATIONS
00905 FOR I=1 TO T1
00910 X=T(I,2)
00915 Y=T(I,3)
00920 S=T(I,1)
00925 B$(X,Y)=N$(S)
00930 NEXT I
00935 FOR I=1 TO 10
00936 I1=I-1
00937 PRINT TAB(10);B$(I1,0);SPC(4);B$(I1,1);SPC(4);B$(I1,2);SPC(4);
00938 PRINT B$(I1,3);SPC(4);B$(I1,4);SPC(4);B$(I1,5);SPC(4);B$(I1,6);
00939 PRINT SPC(4);B$(I1,7);SPC(4);B$(I1,8);SPC(4);B$(I1,9)
00949 NEXT I
00950 PRINT "THANKS FOR PLAYING - COME AGAIN."
00951 PAUSE 
00999 STOP 
01000 M=M-9
01001 ON M GOTO 1005,1010,1015,1020,1025,1030,1035,1040,1045
01005 PRINT "IT SURE IS FUN WHEN YOU HIT A SHIP - BUT YOU MISSED."
01006 GOTO 580
01010 PRINT "GO DIRECTLY TO JAIL. DO NOT PASS GO."
01011 PRINT "OOPS, SORRY! WRONG GAME --- YOU MISSED."
01012 GOTO 580
01015 PRINT "BOO-HOO! YOU SANK MY RUBBER DUCKY - NO POINTS FOR YOU."
01016 GOTO 580
01020 PRINT "THAT WAS THE BEST SHOT SO FAR - BUT - YOU MISSED."
01021 GOTO 580
01025 PRINT "NOW YOU DID IT!    YOU MISSED."
01026 GOTO 580
01030 PRINT "HEY! YOU MISSED. REMEMBER THESE SHELLS ARE NOT TAX-DEDUCTIBLE."
01032 GOTO 580
01035 PRINT "ALL GREAT PEOPLE - EAT CARROTS, HAVE BIG EARS, AND"
01036 PRINT "BELONG TO HUG.  BETTER KNOWN AS HARES UR GREAT."
01037 PRINT "BY THE WAY - YOU MISSED."
01038 GOTO 580
01040 PRINT "DIRECT MISS!"
01041 PAUSE 1000
01042 PRINT "GOTCHA - YOU MISSED."
01043 GOTO 580
01045 PRINT "I'M SORRY TO TELL YOU THIS - BUT - YOU MISSED."
01046 GOTO 580
01099 END 
