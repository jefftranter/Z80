00010 REM H8 CLOCK
00020 J=0:K=0:H=0:M=0:S=0:DIM D(9)
00025 FOR I=0 TO 8:D(I)=255:NEXT I
00030 CNTRL 2,1:REM ALLOW USER UPDATE OF FRONT PANEL
00040 INPUT "ENTER CURRENT TIME AS: HH,MM,SS  ";H,M,S
00050 GOSUB 400:GOSUB 300:GOSUB 200
00100 T=PEEK(8219):REM GET TICCNT
00110 IF T<178 THEN GOTO 100
00115 IF J=0 THEN J=1:GOTO 100
00120 J=0:K=K+1:IF K>1 THEN K=0
00130 IF K<>0 THEN GOTO 100
00140 S=S+1:IF S>59 THEN S=0
00150 GOSUB 200:IF S<>0 THEN GOTO 100
00160 M=M+1:IF M>59 THEN M=0
00170 GOSUB 300:IF M<>0 THEN GOTO 100
00180 H=H+1:IF H>11 THEN H=0
00190 GOSUB 400:GOTO 100
00200 REM UPDATE SECONDS ON FRONT PANEL
00210 C$=STR$(S):D(6)=SEG(0):IF S>9 THEN D(6)=SEG(VAL(LEFT$(C$,2)))
00220 D(7)=SEG(VAL(RIGHT$(C$,2)))
00230 POKE 8209,D(6):POKE 8210,D(7):POKE 8211,D(8)
00235 FOR I=1 TO 30:NEXT I:REM FINE TUNING
00240 PRINT H;":";M;":";S;"   ";CHR$(13);
00250 RETURN 
00300 REM UPDATE MINUTES ON FRONT PANEL
00310 C$=STR$(M):D(4)=SEG(0):IF M>9 THEN D(4)=SEG(VAL(LEFT$(C$,2)))
00320 D(5)=SEG(VAL(RIGHT$(C$,2)))
00330 POKE 8206,D(3):POKE 8207,D(4):POKE 8208,D(5)
00340 RETURN 
00400 REM UPDATE HOURS ON FRONT PANEL
00410 C$=STR$(H):D(1)=SEG(0):IF H>9 THEN D(1)=SEG(VAL(LEFT$(C$,2)))
00420 D(2)=SEG(VAL(RIGHT$(C$,2)))
00430 POKE 8203,D(0):POKE 8204,D(1):POKE 8205,D(2)
00440 RETURN 
