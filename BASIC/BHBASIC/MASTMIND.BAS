00010 GOSUB 1330
00020 PRINT "***************** MASTERMIND *******************"
00030 PRINT :PRINT :
00040 REM     ***** MASTERMIND *****
00050 REM      WRITTEN BY: CLIFF HEATH
00060 REM      912 MARTIN DRIVE
00070 REM      WALLA WALLA, WA 99362          WRITTEN         11/28/77
00080 REM                                     LATEST REVISION 11/16/78
00090 LINE INPUT "DO YOU WANT INSTRUCTIONS? <NO> ";I$
00100 IF LEFT$(I$,1)="Y" THEN 1080
00110 PRINT ,"THIS PROGRAM ASSUMES YOU KNOW THE RULES OF 'MASTERMIND'"
00120 PRINT ,"USING A FOUR COLOR CODE OF THE SIX COLORS AVAILABLE. THE CODE"
00130 PRINT ,"WILL NOT NECESSARILY BE FOUR DIFFERENT COLORS. I PICK THE "
00140 PRINT ,"CODE AND YOU HAVE TEN CHANCES TO GUESS IT. WHEN I ASK FOR "
00150 PRINT ,"YOUR TRIAL CODE, ENTER YOUR GUESS USING THE FIRST LETTER OF"
00160 PRINT ,"EACH COLOR WITHOUT COMMAS BETWEEN THEM. THE COLORS AVAILABLE"
00170 PRINT ,"ARE: RED, YELLOW, BLUE, PINK, ORANGE, AND GREEN. GOOD LUCK!"
00180 A=RND(-PEEK(8219))
00190 G1=0
00200 DIM B$(4),A$(4),A1$(4),B1$(4)
00210 J=0
00220 FOR C=1 TO 4
00230 A=10*RND(C)
00240 A=10*(10*(A-INT(A))-INT(10*(A-INT(A))))
00250 ON INT(A)GOTO 270,280,290,300,310,320
00260 GOTO 230
00270 B$(C)="RED":GOTO 330
00280 B$(C)="YELLOW":GOTO 330
00290 B$(C)="BLUE":GOTO 330
00300 B$(C)="PINK":GOTO 330
00310 B$(C)="ORANGE":GOTO 330
00320 B$(C)="GREEN"
00330 NEXT C
00340 PRINT 
00350 PRINT "RED, YELLOW, BLUE, PINK, ORANGE, GREEN"
00360 PRINT :PRINT TAB(37);"BLACKS  WHITES          TRIAL CODE"
00370 J=J+1
00380 PRINT TAB(62);:LINE INPUT "?  ";F$
00390 G$=LEFT$(F$,1)
00400 GOSUB 1000
00410 A$(1)=G$
00420 G$=MID$(F$,2,1)
00430 GOSUB 1000
00440 A$(2)=G$
00450 G$=MID$(F$,3,1)
00460 GOSUB 1000
00470 A$(3)=G$
00480 G$=RIGHT$(F$,1)
00490 GOSUB 1000
00500 A$(4)=G$
00510 FOR H=1TO 4
00520 B1$(H)=B$(H)
00530 A1$(H)=A$(H)
00540 NEXT H
00550 B=0:W=0
00560 FOR I=1TO 4
00570 IF A1$(I)<>B1$(I)GOTO 610
00580 B=B+1
00590 A1$(I)="A"
00600 B1$(I)="B"
00610 NEXT I
00620 FOR L=1TO 4
00630 FOR M=1TO 4
00640 IF A1$(L)<>B1$(M)GOTO 680
00650 W=W+1
00660 A1$(L)="A"
00670 B1$(M)="B"
00680 NEXT M
00690 NEXT L
00700 PRINT J;TAB(4);A$(1);TAB(12);A$(2);TAB(20);A$(3);TAB(28);A$(4);
00710 PRINT TAB(39);B;TAB(47);W;
00720 IF B<>4 AND J<>10 GOTO 370
00730 IF G1=0 THEN G2=J:GOTO 750
00740 G2=(G2*G1+J)/(G1+1)
00750 G1=G1+1
00760 PRINT 
00770 IF B=4 GOTO 790
00780 IF J=10GOTO 930
00790 ON J GOTO 800,810,820,830,840,850,860,870,880,890
00800 PRINT "YOU ARE UNBELIEVABLY LUCKY!":GOTO 900
00810 PRINT "YOUR SKILL IS ONLY SURPASSED BY YOUR FANTASTIC LUCK!":GOTO 900
00820 PRINT "NICE GAME, BUT MOSTLY LUCK.":GOTO 900
00830 PRINT "CONGRATULATIONS. YOUR LOGICAL PRECISION IS EXEMPLARY.":GOTO 900
00840 PRINT "ANOTHER AVERAGE GAME.":GOTO 900
00850 PRINT "YOU'RE SLIPPING.":GOTO 900
00860 PRINT "SOME MORE PRACTICE MAY HELP.":GOTO 900
00870 PRINT "A BEGINNER, I CAN TELL.":GOTO 900
00880 PRINT "I'LL TRY TO MAKE THE NEXT ONE EASIER. OK?":GOTO 900
00890 PRINT "NO COMMENT COULD BE APPROPRIATE."
00900 LINE INPUT "ANOTHER GAME (Y OR N)? <Y> ";D$
00910 IF LEFT$(D$,1)="N"GOTO 970
00920 GOTO 210
00930 PRINT "YOU LOSE, FELLA. I CAN'T HANDLE MORE THAN TEN LINES."
00940 PRINT "THE HIDDEN CODE WAS:":PRINT ,
00950 FOR C=1TO 4:PRINT B$(C),:NEXT C:PRINT 
00960 PRINT "TRY AGAIN.":GOTO 900
00970 PRINT "YOU SURE GIVE UP EASY!"
00980 PRINT "YOU AVERAGED";G2;"TRIALS TO FIND";G1;"CODES."
00990 PRINT "BETTER LUCK NEXT TIME.":END 
01000 IF G$="R"THEN G$="RED"
01010 IF G$="Y"THEN G$="YELLOW"
01020 IF G$="B"THEN G$="BLUE"
01030 IF G$="P"THEN G$="PINK"
01040 IF G$="O"THEN G$="ORANGE"
01050 IF G$="G"THEN G$="GREEN"
01060 RETURN 
01070 REM INSTRUCTIONS
01080 GOSUB 1330
01090 PRINT "THERE ARE SIX POSSIBLE COLORS (CODES) TO SELECT FROM:"
01100 PRINT ;TAB(20);"RED";TAB(30);"YELLOW";TAB(40);"BLUE"
01110 PRINT ;TAB(20);"PINK";TAB(30);"GREEN";TAB(40);"ORANGE"
01120 PRINT "YOUR JOB IS TO DETERMINE THE CORRECT COLORS AND THE PROPER SEQUENCE."
01130 PRINT :PRINT :
01140 PRINT "HIT RETURN FOR MORE INFORMATION <CR>":PAUSE 
01150 GOSUB 1330
01160 PRINT "AFTER EACH OF YOUR ATTEMPTS TO CRACK THE CODE , I WILL GIVE YOU A HINT."
01170 PRINT "       # OF BLACKS - INDICATE CORRECT COLOR IN PROPER POSITION"
01180 PRINT "       # OF WHITES - INDICATE CORRECT COLOR BUT WRONG POSITION"
01190 PRINT "YOU WILL BE GIVEN 10 ATTEMPTS TO DETERMINE THE PROPER CODE."
01200 PRINT "IF YOU FAIL AFTER 10 TRIES, I WILL REVEAL THE CODE TO YOU."
01210 PRINT "UPON SUCESSFULLY DETERMINING THE CODE, I WILL ALLOW YOU TO PLAY AGAIN."
01220 PRINT :PRINT :
01230 PRINT "HIT RETURN <CR>":PAUSE 
01240 GOSUB 1330
01250 PRINT "ENTER ONLY THE FIRST LETTER OF THE DESIRED COLOR (RED=R).  "
01260 PRINT "FOUR COLORS ARE TO BE CHOOSEN AT TIME (SUCH AS RYBG = RED, YELLOW,"
01270 PRINT "BLUE AND GREEN)."
01280 PRINT :PRINT :
01290 PRINT "HIT THE RETURN KEY TO GET THIS SHOW ON THE ROAD.":PAUSE 
01300 GOSUB 1330
01310 PRINT "   OK, LET'S  PLAY  M A S T E R M I N D =======>"
01320 GOTO 120
01330 FOR I=1 TO 11:PRINT :NEXT I:RETURN 
01340 FOR I=1TO 5:PRINT B$(I),:NEXT 
