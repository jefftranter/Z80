00001 REM  H8/H19/H89 MORSE CODE TUTOR (BENTON HARBOR BASIC)
00002 REM 
00003 REM   10 MAIN MENU           500 ALL CHARACTERS
00004 REM  100 ASCII<->MORSE       600 RANDOM CHARACTERS
00005 REM  200 SEND MORSE CODE     700 CODE GROUPS
00006 REM  300 ENTER TEXT          800 QUIZ
00007 REM  400 WORD SPEED TEST     900 READ TEXT FILE
00008 REM 
00009 CLEAR 
00010 REM *** MAIN MENU ***
00015 I=RND(-PEEK(8219)):REM INITIALIZE RND() FROM TICCNT
00017 H8$="H19/H89":IF H8<>0 THEN H8$="H8"
00020 PRINT CHR$(27);"E";"MORSE CODE TUTOR BY N6YPC":PRINT 
00021 PRINT "1: ALL CHARACTERS"
00022 PRINT "2: RANDOM CHARACTERS"
00023 PRINT "3: CODE GROUPS"
00024 PRINT "4: ENTER TEXT"
00025 PRINT "5: READ TEXT FILE"
00026 PRINT "6: QUIZ"
00027 PRINT "7: WORD SPEED TEST"
00028 PRINT "8: SPEAKER OUTPUT <";H8$;">"
00030 PRINT :PRINT "SELECT (1-8): ";
00035 IF CIN(0)>0 THEN 35:REM CLEAR BUFFER
00040 I=CIN(0):IF I<0 THEN 40
00045 I$=CHR$(I)
00050 IF I$="" THEN 40
00051 IF I$="1" THEN 500
00052 IF I$="2" THEN 600
00053 IF I$="3" THEN 700
00054 IF I$="4" THEN 300
00055 IF I$="5" THEN 900
00056 IF I$="6" THEN 800
00057 IF I$="7" THEN 400
00058 IF I$="8" THEN H8=NOT H8
00060 GOTO 10
00100 REM *** ASCII<->MORSE ***
00101 DATA "A",".-"
00102 DATA "B","-..."
00103 DATA "C","-.-."
00104 DATA "D","-.."
00105 DATA "E","."
00106 DATA "F","..-."
00107 DATA "G","--."
00108 DATA "H","...."
00109 DATA "I",".."
00110 DATA "J",".---"
00111 DATA "K","-.-"
00112 DATA "L",".-.."
00113 DATA "M","--"
00114 DATA "N","-."
00115 DATA "O","---"
00116 DATA "P",".--."
00117 DATA "Q","--.-"
00118 DATA "R",".-."
00119 DATA "S","..."
00120 DATA "T","-"
00121 DATA "U","..-"
00122 DATA "V","...-"
00123 DATA "W",".--"
00124 DATA "X","-..-"
00125 DATA "Y","-.--"
00126 DATA "Z","--.."
00127 DATA "1",".----"
00128 DATA "2","..---"
00129 DATA "3","...--"
00130 DATA "4","....-"
00131 DATA "5","....."
00132 DATA "6","-...."
00133 DATA "7","--..."
00134 DATA "8","---.."
00135 DATA "9","----."
00136 DATA "0","-----"
00137 DATA "AR",".-.-."
00138 DATA "BT","-...-"
00139 DATA "SK","...-.-"
00140 DATA "/","-..-."
00141 DATA ".",".-.-.-"
00142 DATA ",","--..--"
00143 DATA "?","..--.."
00144 DATA "@",".--.-."
00145 DATA "",""
00200 REM *** SEND MORSE CODE ***
00203 IF H8=0 THEN 250
00205 D1=16:D3=D1*3:REM SPEAKER OUTPUT = H8
00210 FOR N=1 TO LEN(M$)
00215 POKE 8201,PEEK(8201) AND 127:OUT 249,32:REM SOUND ON
00220 IF MID$(M$,N,1)="-"THEN 230
00225 PAUSE D1:GOTO 235
00230 PAUSE D3
00235 POKE 8201,PEEK(8201) OR 128:OUT 249,0:REM SOUND OFF
00240 PAUSE D1:NEXT N
00245 PAUSE D3:RETURN 
00250 D1=4:D3=D1*3:REM SPEAKER OUTPUT = H19/H89
00255 FOR N=1 TO LEN(M$)
00260 OUT 249,32:REM RELAY ON
00265 IF MID$(M$,N,1)="-"THEN 275
00270 FOR P=0 TO D1: PRINT CHR$(7);: NEXT P:GOTO 280
00275 FOR P=0 TO D3: PRINT CHR$(7);: NEXT P
00280 OUT 249,0:REM RELAY OFF
00285 FOR P=0 TO D1:NEXT N
00290 FOR P=0 TO D3:RETURN 
00300 REM *** ENTER STRING ***
00302 PRINT CHR$(27);"E";
00303 PRINT "ENTER TEXT OR PRESS <RETURN> TO QUIT";CHR$(13);CHR$(10);
00304 IF CIN(0)>0 THEN 304:REM CLEAR BUFFER
00306 I=CIN(0):IF I<0 THEN 306
00308 IF I=13 OR I=10 THEN 10
00310 LINE INPUT ;B$:B$=CHR$(I)+B$
00315 PRINT CHR$(8);CHR$(8);" "
00320 GOSUB 325:GOTO 300 
00325 IF LEN(B$)=0 THEN 10
00330 FOR J=1 TO LEN(B$)
00335 C$=MID$(B$,J,1)
00337 IF C$>=CHR$(97) THEN IF C$<=CHR$(122) THEN C$=CHR$(ASC(C$)-32)
00340 PRINT C$;:RESTORE 
00345 IF C$<>" "THEN GOTO 355
00350 J=J+1:GOSUB 245:GOTO 335
00355 FOR K=1 TO 44
00360 READ A$,M$
00365 IF A$<>C$ THEN NEXT K
00370 IF K<45 THEN GOSUB 200
00375 NEXT J
00380 RETURN 
00400 REM *** WORD SPEED TEST ***
00405 PRINT CHR$(27);"E";
00410 B$=" PARIS"
00415 POKE 8220,0:T=PEEK(8220)+110
00420 FOR I=1 TO 25
00425 GOSUB 325
00430 IF T>PEEK(8220) THEN NEXT I
00435 PRINT :PRINT :PRINT I;"WORDS PER MINUTE.  PRESS <RETURN> FOR MENU";
00437 IF CIN(0)>0 THEN 437:REM CLEAR BUFFER
00440 IF CIN(0)<0 THEN 440
00445 GOTO 10
00500 REM *** ALL CHARACTERS ***
00505 IF CIN(0)>0 THEN 505:REM CLEAR BUFFER
00510 PRINT CHR$(27);"E";"PRESS <RETURN> TO QUIT":PRINT 
00515 RESTORE 
00520 FOR I=1 TO 44
00525 READ A$,M$
00530 PRINT A$;" ";M$
00535 GOSUB 200 :GOSUB 245
00537 FOR P=0 TO 100:NEXT P
00540 IF CIN(0)<0 THEN NEXT I
00545 GOTO 10
00600 REM *** RANDOM CHARACTERS ***
00605 IF CIN(0)>0 THEN 605:REM CLEAR BUFFER
00610 PRINT CHR$(27);"E";"PRESS <RETURN> TO QUIT":PRINT 
00615 RESTORE 
00620 FOR I=1 TO RND(1)*45
00625 READ A$,M$
00630 NEXT I
00635 PRINT A$;" ";M$
00640 GOSUB 200 :GOSUB 245
00642 FOR P=0 TO 75:NEXT P
00645 IF CIN(0)<0 THEN 615
00650 GOTO 10
00700 REM *** CODE GROUPS ***
00710 PRINT CHR$(27);"E";
00720 PRINT "CODE GROUPS":PRINT 
00721 PRINT "GROUP 1: ETIMAN     ";
00722 PRINT "GROUP 6: HJCZ"
00723 PRINT "GROUP 2: SOUD       ";
00724 PRINT "GROUP 7: 12345"
00725 PRINT "GROUP 3: RKWG       ";
00726 PRINT "GROUP 8: 67890"
00727 PRINT "GROUP 4: VBFL       ";
00728 PRINT "GROUP 9: .,?/@"
00729 PRINT "GROUP 5: PXQY       "
00730 REM PRINT "GROUP 0: AR BT SK"
00740 PRINT :PRINT :PRINT "OR PRESS <RETURN> TO QUIT"
00741 PRINT CHR$(27);"A";CHR$(27);"A";"SELECT GROUP (1-9): ";
00743 IF CIN(0)>0 THEN 743:REM CLEAR BUFFER
00745 I=CIN(0):IF I<0 THEN 745
00746 IF I=13 OR I=10 THEN 10
00747 I$=CHR$(I)
00750 IF I$=""THEN 745
00755 PRINT 
00760 REM IF I$="0"THEN D$=" AR BT SK"
00761 IF I$="1" THEN D$=" E T I M A N"
00762 IF I$="2" THEN D$=" S O U D"
00763 IF I$="3" THEN D$=" R K W G"
00764 IF I$="4" THEN D$=" V B F L"
00765 IF I$="5" THEN D$=" P X Q Y"
00766 IF I$="6" THEN D$=" H J C Z"
00767 IF I$="7" THEN D$=" 1 2 3 4 5"
00768 IF I$="8" THEN D$=" 6 7 8 9 0"
00769 IF I$="9" THEN D$=" . , ? / @"
00770 PRINT :FOR G=1 TO LEN(D$)/2
00775 B$=MID$(D$,G*2-1,2)
00780 FOR H=1 TO 5:GOSUB 325:NEXT H
00785 PRINT :NEXT G
00790 PRINT :B$=D$:GOSUB 325
00795 GOTO 700
00800 REM *** QUIZ ***
00805 PRINT CHR$(27);"E";"PRESS <RETURN> TO QUIT":PRINT 
00810 J=0:K=0
00815 RESTORE 
00820 FOR I=1 TO RND(1)*37
00825 READ A$,M$
00830 NEXT I
00835 PRINT M$,:GOSUB 200
00837 IF CIN(0)>0 THEN 837:REM CLEAR BUFFER
00840 I=CIN(0): IF I<0 THEN 840
00842 I$=CHR$(I)
00845 PRINT CHR$(27);"A";CHR$(9);CHR$(9);"  ";
00850 IF ASC(I$)>95 THEN I$=CHR$(ASC(I$) AND 95)
00855 IF I$<>A$ THEN 865
00860 PRINT "CORRECT!" :J=J+1:GOTO 815
00865 IF I$<"0" OR I$>"Z" THEN 875
00870 PRINT "INCORRECT, TRY AGAIN" :K=K+1:GOTO 835
00875 IF CIN(0)>0 THEN 875:REM CLEAR BUFFER
00880 IF J+K=0 THEN 10
00885 PRINT :PRINT :PRINT INT(100*(J/(J+K)));CHR$(8);
00887 PRINT "% CORRECT.  PRESS <RETURN> FOR MENU";
00890 IF CIN(0)<0 THEN 890
00895 GOTO 10
00900 REM *** READ FILE ***
00905 I$="MORSE2TR.DAT"
00910 PRINT CHR$(27);"E";"FILE <";I$;">: ";
00915 IF CIN(0)>0 THEN 915:REM CLEAR BUFFER
00920 I=CIN(0):IF I<0 THEN 920
00925 IF I=13 OR I=10 THEN 940
00930 LINE INPUT ;I$:I$=CHR$(I)+I$
00935 PRINT CHR$(8);CHR$(8);" ";
00940 IF CIN(0)>0 THEN 940:REM CLEAR BUFFER
00945 OPEN I$ FOR READ AS FILE #1
00950 PRINT CHR$(27);"E";"PRESS <RETURN> TO QUIT AT END OF LINE":PRINT 
00955 I=CIN(1): IF I<=0 OR CIN(0)>0 THEN CLOSE #1:GOTO 10:REM READ UNTIL EOF
00960 LINE INPUT #1,;B$:B$=CHR$(I)+B$
00965 GOSUB 330:PRINT CHR$(13)+CHR$(10);:GOTO 955
00999 END 
