00005 PRINT CHR$(27);"E";
00010 PRINT TAB(33);"TOWERS"
00020 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
00030 PRINT :PRINT :PRINT 
00090 PRINT 
00100 REM *** INITIALIZE
00110 DIM T(7,3)
00120 E=0
00130 FOR D=1 TO 7
00140 FOR N=1 TO 3
00150 T(D,N)=0
00160 NEXT N
00170 NEXT D
00180 PRINT "TOWERS OF HANOI PUZZLE.": PRINT 
00200 PRINT "YOU MUST TRANSFER THE DISKS FROM THE LEFT TO THE RIGHT"
00205 PRINT "TOWER, ONE AT A TIME, NEVER PUTTING A LARGER DISK ON A"
00210 PRINT "SMALLER DISK.": PRINT 
00215 INPUT "HOW MANY DISKS DO YOU WANT TO MOVE (7 IS MAX)? ";S
00220 PRINT 
00230 M=0
00240 FOR Q=1 TO 7
00250 IF Q=S THEN 350
00260 NEXT Q
00270 E=E+1
00280 IF E>2 THEN 310
00290 PRINT "SORRY, BUT I CAN'T DO THAT JOB FOR YOU.": GOTO 215
00310 PRINT "ALL RIGHT, WISE GUY, IF YOU CAN'T PLAY THE GAME RIGHT, I'LL"
00320 PRINT "JUST TAKE MY PUZZLE AND GO HOME.  SO LONG.": STOP 
00340 REM *** STORE DISKS FROM SMALLEST TO LARGEST
00350 PRINT "IN THIS PROGRAM, WE SHALL REFER TO DISKS BY NUMERICAL CODE."
00355 PRINT "3 WILL REPRESENT THE SMALLEST DISK, 5 THE NEXT SIZE,"
00360 PRINT "7 THE NEXT, AND SO ON, UP TO 15.  IF YOU DO THE PUZZLE WITH"
00365 PRINT "2 DISKS, THEIR CODE NAMES WOULD BE 13 AND 15.  WITH 3 DISKS"
00370 PRINT "THE CODE NAMES WOULD BE 11, 13 AND 15, ETC.  THE NEEDLES"
00375 PRINT "ARE NUMBERED FROM LEFT TO RIGHT, 1 TO 3.  WE WILL"
00380 PRINT "START WITH THE DISKS ON NEEDLE 1, AND ATTEMPT TO MOVE THEM"
00385 PRINT "TO NEEDLE 3."
00390 PRINT : PRINT "GOOD LUCK!": PRINT 
00400 Y=7: D=15
00420 FOR X=S TO 1 STEP -1
00430 T(Y,1)=D: D=D-2: Y=Y-1
00460 NEXT X
00470 GOSUB 1230
00480 PRINT :PRINT "WHICH DISK WOULD YOU LIKE TO MOVE";:E=0
00500 INPUT D
00510 IF (D-3)*(D-5)*(D-7)*(D-9)*(D-11)*(D-13)*(D-15)=0 THEN 580
00520 PRINT "ILLEGAL ENTRY... YOU MAY ONLY TYPE 3,5,7,9,11,13, OR 15."
00530 E=E+1: IF E>1 THEN 560
00550 GOTO 500
00560 PRINT "STOP WASTING MY TIME.  GO BOTHER SOMEONE ELSE.": STOP 
00580 REM *** CHECK IF REQUESTED DISK IS BELOW ANOTHER
00590 FOR R=1 TO 7
00600 FOR C=1 TO 3
00610 IF T(R,C)=D THEN 640
00620 NEXT C: NEXT R
00640 FOR Q=R TO 1 STEP -1
00645 IF T(Q,C)=0 THEN 660
00650 IF T(Q,C)<D THEN 680
00660 NEXT Q
00670 GOTO 700
00680 PRINT "THAT DISK IS BELOW ANOTHER ONE.  MAKE ANOTHER CHOICE."
00690 GOTO 480
00700 E=0
00705 INPUT "PLACE DISK ON WHICH NEEDLE? ";N
00710 PRINT 
00730 IF (N-1)*(N-2)*(N-3)=0 THEN 800
00735 E=E+1
00740 IF E>1 THEN 780
00750 PRINT "I'LL ASSUME YOU HIT THE WRONG KEY THIS TIME.  BUT WATCH IT,"
00760 PRINT "I ONLY ALLOW ONE MISTAKE.": GOTO 705
00780 PRINT "I TRIED TO WARN YOU, BUT YOU WOULDN'T LISTEN."
00790 PRINT "BYE BYE, BIG SHOT.":STOP 
00800 FOR R=1 TO 7
00810 IF T(R,N)<>0 THEN 840
00820 NEXT R
00830 GOTO 880
00835 REM *** CHECK IF DISK TO BE PLACED ON A LARGER ONE
00840 IF D<T(R,N) THEN 880
00850 PRINT "YOU CAN'T PLACE A LARGER DISK ON TOP OF A SMALLER ONE,"
00860 PRINT "IT MIGHT CRUSH IT!": PRINT "NOW THEN, ";:GOTO 480
00875 REM *** MOVE RELOCATED DISK
00880 FOR V=1 TO 7: FOR W=1 TO 3
00900 IF T(V,W)=D THEN 930
00910 NEXT W: NEXT V
00925 REM *** LOCATE EMPTY SPACE ON NEEDLE N
00930 FOR U=1 TO 7
00940 IF T(U,N)<>0 THEN 970
00950 NEXT U
00960 U=7: GOTO 980
00965 REM *** MOVE DISK AND SET OLD LOCATION TO 0
00970 U=U-1
00980 T(U,N)=T(V,W): T(V,W)=0
00995 REM *** PRINT OUT CURRENT STATUS
01000 GOSUB 1230
01018 REM *** CHECK IF DONE
01020 M=M+1
01030 FOR R=1 TO 7: FOR C=1 TO 2
01050 IF T(R,C)<>0 THEN 1090
01060 NEXT C: NEXT R
01080 GOTO 1120
01090 IF M<=128 THEN 480
01100 PRINT "SORRY, BUT I HAVE ORDERS TO STOP IF YOU MAKE MORE THAN"
01110 PRINT "128 MOVES.": STOP 
01120 IF M<>2^S-1 THEN 1140
01130 PRINT :PRINT "CONGRATULATIONS!!":PRINT 
01140 PRINT "YOU HAVE PERFORMED THE TASK IN";M;"MOVES."
01150 PRINT :INPUT "TRY AGAIN (YES OR NO)? "; A$
01160 IF A$="NO" THEN 1390
01170 IF A$="YES" THEN PRINT : GOTO 120
01180 PRINT : PRINT "'YES' OR 'NO' PLEASE";: INPUT A$: GOTO 1160
01230 REM *** PRINT SUBROUTINE
01240 FOR K=1 TO 7
01250 Z=10
01260 FOR J=1 TO 3
01270 IF T(K,J)=0 THEN 1330
01280 PRINT TAB(Z-INT(T(K,J)/2));
01290 FOR V=1 TO T(K,J)
01300 PRINT "*";
01310 NEXT V
01320 GOTO 1340
01330 PRINT TAB(Z);"*";
01340 Z=Z+21
01350 NEXT J
01360 PRINT 
01370 NEXT K
01380 RETURN 
01390 PRINT : PRINT "THANKS FOR THE GAME!": PRINT : END 
