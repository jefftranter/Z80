1 PRINT TAB(32);"3D PLOT"
2 PRINT TAB(15);"CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
3 PRINT:PRINT:PRINT
5 REM DEF FNA(Z)=30*EXP(-Z*Z/100)
8 REM UPDATED TO PRINT ALL 6 EXAMPLES FROM THE BOOK
10 FOR I= 1 TO 6
20 IF I = 1 THEN DEF FNA(Z)=30*EXP(-Z*Z/100) : GOTO 100
30 IF I = 2 THEN DEF FNA(Z)=SQR(900.01-Z*Z)*.9-2 : GOTO 100
40 IF I = 3 THEN DEF FNA(Z)=30*(COS(Z/16)) : GOTO 100
50 IF I = 4 THEN DEF FNA(Z)=30-30*SIN(Z/18) : GOTO 100
60 IF I = 5 THEN DEF FNA(Z)=30*EXP(-COS(Z/16))-30 : GOTO 100
70 IF I = 6 THEN DEF FNA(Z)=30*SIN(Z/10)
100 PRINT
110 FOR X=-30 TO 30 STEP 1.5
120 L=0
130 Y1=5*INT(SQR(900-X*X)/5)
140 FOR Y=Y1 TO -Y1 STEP -5
150 Z=INT(25+FNA(SQR(X*X+Y*Y))-.7*Y)
160 IF Z<=L THEN 190
170 L=Z
180 PRINT TAB(Z);"*";
190 NEXT Y
200 PRINT
210 NEXT X
220 NEXT I
300 END
